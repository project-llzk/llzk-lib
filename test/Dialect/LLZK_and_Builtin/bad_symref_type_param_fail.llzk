// RUN: llzk-opt -split-input-file -verify-diagnostics %s

// Several tests refer back to this:
//  NOTE: Cannot directly test the unknown symbol check in `verifySymbolUses()` for some ops
//  because the check producing "different type than prior uses" (which uses strict equality
//  rather than type unification) always occurs before `verifySymbolUses()` and there is no
//  (or at least shouldn't be a) way to create an SSA value with a symbol-parameterized type
//  using an unknown symbol due to sufficient `verifySymbolUses()` checks in all other ops.
//  We have tests for these to ensure no regression comes up in the future.

module attributes {llzk.lang} {
  // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
  // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
  function.def private @in_return_struct() -> !struct.type<@Unknown>
}
// -----
module attributes {llzk.lang} {
  // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
  function.def private @in_return_array_of_scalar() -> !array.type<@Unknown x index>
}
// -----
module attributes {llzk.lang} {
  // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
  // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
  function.def private @in_return_array_of_struct() -> !array.type<2 x !struct.type<@Unknown>>
}
// -----
module attributes {llzk.lang} {
  struct.def @A<[@T]> {
    function.def @compute() -> !struct.type<@A<[@T]>> {
      %self = struct.new : !struct.type<@A<[@T]>>
      function.return %self : !struct.type<@A<[@T]>>
    }
    function.def @constrain(%self: !struct.type<@A<[@T]>>) { function.return }
  }

  // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
  function.def private @in_return_deep() ->
    !array.type<2 x !struct.type<@A<[
      !array.type<2 x !struct.type<@A<[
        !struct.type<@A<[
          !array.type<2 x !struct.type<@A<[
            !struct.type<@A<[!poly.tvar<@Unknown>]>>
          ]>>>
        ]>>
      ]>>>
    ]>>>
}
// -----
module attributes {llzk.lang} {
  // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
  // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
  function.def private @in_arg_struct(!struct.type<@Unknown>) -> i1
}
// -----
module attributes {llzk.lang} {
  // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
  function.def private @in_arg_array_of_scalar(!array.type<@Unknown x index>) -> i1
}
// -----
module attributes {llzk.lang} {
  // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
  // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
  function.def private @in_arg_array_of_struct(!array.type<2 x !struct.type<@Unknown>>) -> i1
}
// -----
module attributes {llzk.lang} {
  struct.def @A<[@T]> {
    function.def @compute() -> !struct.type<@A<[@T]>> {
      %self = struct.new : !struct.type<@A<[@T]>>
      function.return %self : !struct.type<@A<[@T]>>
    }
    function.def @constrain(%self: !struct.type<@A<[@T]>>) { function.return }
  }

  // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
  function.def private @in_arg_deep(
    !array.type<2 x !struct.type<@A<[
      !struct.type<@A<[
        !array.type<2 x !struct.type<@A<[
          !array.type<2 x !struct.type<@A<[
            !struct.type<@A<[!poly.tvar<@Unknown>]>>
          ]>>>
        ]>>>
      ]>>
    ]>>>) -> i1

}
// -----
module attributes {llzk.lang} {
  struct.def @InComputeReturnStruct<[@N]> {
    // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
    function.def @compute() -> !struct.type<@InComputeReturnStruct<[@Unknown]>> {
      %self = struct.new : !struct.type<@InComputeReturnStruct<[@N]>>
      function.return %self : !struct.type<@InComputeReturnStruct<[@N]>>
    }
    function.def @constrain(%self: !struct.type<@InComputeReturnStruct<[@N]>>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InComputeReturnArrayOfScalar<[@N]> {
    // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
    function.def @compute() -> !struct.type<@InComputeReturnArrayOfScalar<[!array.type<@Unknown x i1>]>> {
      %self = struct.new : !struct.type<@InComputeReturnArrayOfScalar<[@N]>>
      function.return %self : !struct.type<@InComputeReturnArrayOfScalar<[@N]>>
    }
    function.def @constrain(%self: !struct.type<@InComputeReturnArrayOfScalar<[@N]>>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InComputeReturnArrayOfStruct<[@N]> {
    // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
    // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
    function.def @compute() -> !struct.type<@InComputeReturnArrayOfStruct<[!array.type<1 x !struct.type<@Unknown>>]>> {
      %self = struct.new : !struct.type<@InComputeReturnArrayOfStruct<[@N]>>
      function.return %self : !struct.type<@InComputeReturnArrayOfStruct<[@N]>>
    }
    function.def @constrain(%self: !struct.type<@InComputeReturnArrayOfStruct<[@N]>>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InComputeArgStruct {
    // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
    // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
    function.def @compute(%0: !struct.type<@Unknown>) -> !struct.type<@InComputeArgStruct> {
      %self = struct.new : !struct.type<@InComputeArgStruct>
      function.return %self : !struct.type<@InComputeArgStruct>
    }
    function.def @constrain(%self: !struct.type<@InComputeArgStruct>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InComputeArgArrayOfScalar {
    // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
    function.def @compute(%0: !array.type<@Unknown x index>) -> !struct.type<@InComputeArgArrayOfScalar> {
      %self = struct.new : !struct.type<@InComputeArgArrayOfScalar>
      function.return %self : !struct.type<@InComputeArgArrayOfScalar>
    }
    function.def @constrain(%self: !struct.type<@InComputeArgArrayOfScalar>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InComputeArgArrayOfStruct {
    // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
    // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
    function.def @compute(%0: !array.type<2 x !struct.type<@Unknown>>) -> !struct.type<@InComputeArgArrayOfStruct> {
      %self = struct.new : !struct.type<@InComputeArgArrayOfStruct>
      function.return %self : !struct.type<@InComputeArgArrayOfStruct>
    }
    function.def @constrain(%self: !struct.type<@InComputeArgArrayOfStruct>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InConstrainArgStruct {
    function.def @compute() -> !struct.type<@InConstrainArgStruct> {
      %self = struct.new : !struct.type<@InConstrainArgStruct>
      function.return %self : !struct.type<@InConstrainArgStruct>
    }
    // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
    // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
    function.def @constrain(%self: !struct.type<@InConstrainArgStruct>, %0: !struct.type<@Unknown>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InConstrainArgArrayOfScalar {
    function.def @compute() -> !struct.type<@InConstrainArgArrayOfScalar> {
      %self = struct.new : !struct.type<@InConstrainArgArrayOfScalar>
      function.return %self : !struct.type<@InConstrainArgArrayOfScalar>
    }
    // expected-error@+1 {{'function.def' op references unknown symbol "@Unknown"}}
    function.def @constrain(%self: !struct.type<@InConstrainArgArrayOfScalar>, %0: !array.type<@Unknown x index>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InConstrainArgArrayOfStruct {
    function.def @compute() -> !struct.type<@InConstrainArgArrayOfStruct> {
      %self = struct.new : !struct.type<@InConstrainArgArrayOfStruct>
      function.return %self : !struct.type<@InConstrainArgArrayOfStruct>
    }
    // expected-error@+2 {{'function.def' op references unknown symbol "@Unknown"}}
    // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
    function.def @constrain(%self: !struct.type<@InConstrainArgArrayOfStruct>, %0: !array.type<2 x !struct.type<@Unknown>>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InReturnOp {
    function.def @compute() -> !struct.type<@InReturnOp> {
      %self = struct.new : !struct.type<@InReturnOp> // expected-note {{prior use here}}
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%self' expects different type than prior uses: '!struct.type<@Unknown>' vs '!struct.type<@InReturnOp>'}}
      function.return %self : !struct.type<@Unknown>
    }
    function.def @constrain(%self: !struct.type<@InReturnOp>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InReturnOpAndTypeAttr {
    function.def @compute() -> !struct.type<@InReturnOpAndTypeAttr> {
      %self = struct.new : !struct.type<@Unknown>
      // expected-error@+1 {{type of return operand 0 ('!struct.type<@Unknown>') doesn't match function result type ('!struct.type<@InReturnOpAndTypeAttr>') in function @compute}}
      function.return %self : !struct.type<@Unknown>
    }
    function.def @constrain(%self: !struct.type<@InReturnOpAndTypeAttr>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  function.def private @id(%a: !felt.type) -> !felt.type
  function.def @callop_unknown_in_return_type(%a: !felt.type) {
    // expected-error@+1 {{'function.call' op references unknown symbol "@Unknown"}}
    %sum = function.call @id(%a) : (!felt.type) -> (!poly.tvar<@Unknown>)
    function.return
  }
}
// -----
module attributes {llzk.lang} {
  function.def private @id(%a: !felt.type) -> !felt.type
  function.def @callop_unknown_in_return_type(%a: !felt.type) { // expected-note {{prior use here}}
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!poly.tvar<@Unknown>' vs '!felt.type'}}
    %sum = function.call @id(%a) : (!poly.tvar<@Unknown>) -> (!felt.type)
    function.return
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InNewStruct {
    function.def @compute() -> !struct.type<@InNewStruct> {
      %self = struct.new : !struct.type<@InNewStruct>
      // expected-error@+2 {{'struct.new' op references unknown symbol "@Unknown"}}
      // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
      %u = struct.new : !struct.type<@Unknown>
      function.return %self : !struct.type<@InNewStruct>
    }
    function.def @constrain(%self: !struct.type<@InNewStruct>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InNewStructParam<[@A]> {
    function.def @compute() -> !struct.type<@InNewStructParam<[@A]>> {
      %self = struct.new : !struct.type<@InNewStructParam<[@A]>>
      // expected-error@+1 {{'struct.new' op references unknown symbol "@Unknown"}}
      %u = struct.new : !struct.type<@InNewStructParam<[@Unknown]>>
      function.return %self : !struct.type<@InNewStructParam<[@A]>>
    }
    function.def @constrain(%self: !struct.type<@InNewStructParam<[@A]>>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @InNewStructArrayParam<[@A]> {
    function.def @compute() -> !struct.type<@InNewStructArrayParam<[@A]>> {
      %self = struct.new : !struct.type<@InNewStructArrayParam<[@A]>>
      // expected-error@+1 {{'struct.new' op references unknown symbol "@Unknown"}}
      %u = struct.new : !struct.type<@InNewStructArrayParam<[!array.type<@Unknown x i1>]>>
      function.return %self : !struct.type<@InNewStructArrayParam<[@A]>>
    }
    function.def @constrain(%self: !struct.type<@InNewStructArrayParam<[@A]>>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @MemberDefOpWithUnknownScalarArray {
    // expected-error@+1 {{'struct.member' op references unknown symbol "@Unknown"}}
    struct.member @ret: !array.type<@Unknown x !felt.type>
    function.def @compute() -> !struct.type<@MemberDefOpWithUnknownScalarArray> {
      %self = struct.new : !struct.type<@MemberDefOpWithUnknownScalarArray>
      function.return %self : !struct.type<@MemberDefOpWithUnknownScalarArray>
    }
    function.def @constrain(%self: !struct.type<@MemberDefOpWithUnknownScalarArray>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @MemberDefOpWithUnknownTypeVar {
    // expected-error@+1 {{'struct.member' op references unknown symbol "@Unknown"}}
    struct.member @ret: !poly.tvar<@Unknown>
    function.def @compute() -> !struct.type<@MemberDefOpWithUnknownTypeVar> {
      %self = struct.new : !struct.type<@MemberDefOpWithUnknownTypeVar>
      function.return %self : !struct.type<@MemberDefOpWithUnknownTypeVar>
    }
    function.def @constrain(%self: !struct.type<@MemberDefOpWithUnknownTypeVar>) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @ConstReadWithUnknown<[@T]> {
    function.def @compute() -> !struct.type<@ConstReadWithUnknown<[@T]>> {
      %self = struct.new : !struct.type<@ConstReadWithUnknown<[@T]>>
      function.return %self : !struct.type<@ConstReadWithUnknown<[@T]>>
    }
    function.def @constrain(%self: !struct.type<@ConstReadWithUnknown<[@T]>>) {
      // expected-error@+1 {{'poly.read_const' op references unknown symbol "@Unknown"}}
      %0 = poly.read_const @T : !poly.tvar<@Unknown>
      function.return
    }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @MemberReadWithUnknownInBase<[@A]> {
    struct.member @f: !felt.type
    function.def @compute() -> !struct.type<@MemberReadWithUnknownInBase<[@A]>> {
      %self = struct.new : !struct.type<@MemberReadWithUnknownInBase<[@A]>>
      function.return %self : !struct.type<@MemberReadWithUnknownInBase<[@A]>>
    }
    function.def @constrain(%self: !struct.type<@MemberReadWithUnknownInBase<[@A]>>) { // expected-note {{prior use here}}
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%self' expects different type than prior uses: '!struct.type<@MemberReadWithUnknownInBase<[@Unknown]>>' vs '!struct.type<@MemberReadWithUnknownInBase<[@A]>>'}}
      %0 = struct.readm %self[@f] : !struct.type<@MemberReadWithUnknownInBase<[@Unknown]>>, !felt.type
      function.return
    }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @MemberReadWithUnknownInResultTypeVar {
    struct.member @f: index
    function.def @compute() -> !struct.type<@MemberReadWithUnknownInResultTypeVar> {
      %self = struct.new : !struct.type<@MemberReadWithUnknownInResultTypeVar>
      function.return %self : !struct.type<@MemberReadWithUnknownInResultTypeVar>
    }
    function.def @constrain(%self: !struct.type<@MemberReadWithUnknownInResultTypeVar>) {
      // expected-error@+1 {{'struct.readm' op references unknown symbol "@Unknown"}}
      %0 = struct.readm %self[@f] : !struct.type<@MemberReadWithUnknownInResultTypeVar>, !poly.tvar<@Unknown>
      function.return
    }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @MemberReadWithUnknownInResultArr<[@A]> {
    struct.member @f: !array.type<@A x i1>
    function.def @compute() -> !struct.type<@MemberReadWithUnknownInResultArr<[@A]>> {
      %self = struct.new : !struct.type<@MemberReadWithUnknownInResultArr<[@A]>>
      function.return %self : !struct.type<@MemberReadWithUnknownInResultArr<[@A]>>
    }
    function.def @constrain(%self: !struct.type<@MemberReadWithUnknownInResultArr<[@A]>>) {
      // expected-error@+1 {{'struct.readm' op references unknown symbol "@Unknown"}}
      %0 = struct.readm %self[@f] : !struct.type<@MemberReadWithUnknownInResultArr<[@A]>>, !array.type<@Unknown x i1>
      function.return
    }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @MemberWriteWithUnknownInBase<[@A]> {
    struct.member @f: !felt.type
    function.def @compute(%0: i1) -> !struct.type<@MemberWriteWithUnknownInBase<[@A]>> {
      %self = struct.new : !struct.type<@MemberWriteWithUnknownInBase<[@A]>>
      function.return %self : !struct.type<@MemberWriteWithUnknownInBase<[@A]>>
    }
    function.def @constrain(%self: !struct.type<@MemberWriteWithUnknownInBase<[@A]>>, %0: i1) { // expected-note {{prior use here}}
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%self' expects different type than prior uses: '!struct.type<@MemberWriteWithUnknownInBase<[@Unknown]>>' vs '!struct.type<@MemberWriteWithUnknownInBase<[@A]>>'}}
      struct.writem %self[@f] = %0 : !struct.type<@MemberWriteWithUnknownInBase<[@Unknown]>>, i1
      function.return
    }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @MemberWriteWithUnknownInRHSTypeVar<[@A]> {
    struct.member @f: !poly.tvar<@A>
    function.def @compute(
      %0: !poly.tvar<@A> // expected-note {{prior use here}}
    ) -> !struct.type<@MemberWriteWithUnknownInRHSTypeVar<[@A]>> {
      %self = struct.new : !struct.type<@MemberWriteWithUnknownInRHSTypeVar<[@A]>>
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%0' expects different type than prior uses: '!poly.tvar<@Unknown>' vs '!poly.tvar<@A>'}}
      struct.writem %self[@f] = %0 : !struct.type<@MemberWriteWithUnknownInRHSTypeVar<[@A]>>, !poly.tvar<@Unknown>
      function.return %self : !struct.type<@MemberWriteWithUnknownInRHSTypeVar<[@A]>>
    }
    function.def @constrain(%self: !struct.type<@MemberWriteWithUnknownInRHSTypeVar<[@A]>>) {
      function.return
    }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @MemberWriteWithUnknownInRHSArr<[@A]> {
    struct.member @f: !array.type<@A x i1>
    function.def @compute(
      %0: !array.type<@A x i1> // expected-note {{prior use here}}
    ) -> !struct.type<@MemberWriteWithUnknownInRHSArr<[@A]>> {
      %self = struct.new : !struct.type<@MemberWriteWithUnknownInRHSArr<[@A]>>
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%0' expects different type than prior uses: '!array.type<@Unknown x i1>' vs '!array.type<@A x i1>'}}
      struct.writem %self[@f] = %0 : !struct.type<@MemberWriteWithUnknownInRHSArr<[@A]>>, !array.type<@Unknown x i1>
      function.return %self : !struct.type<@MemberWriteWithUnknownInRHSArr<[@A]>>
    }
    function.def @constrain(%self: !struct.type<@MemberWriteWithUnknownInRHSArr<[@A]>>) {
      function.return
    }
  }
}
// -----
module attributes {llzk.lang} {
  function.def @create_array_op_unknown() {
    // expected-error@+1 {{'array.new' op references unknown symbol "@Unknown"}}
    %arr = array.new : !array.type<@Unknown x !felt.type>
    function.return
  }
}
// -----
module attributes {llzk.lang} {
  function.def @ReadArrayOp_unknown_base(
    %arr: !array.type<5 x !felt.type>, // expected-note {{prior use here}}
    %k: index
  ) {
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%arr' expects different type than prior uses: '!array.type<@Unknown x !felt.type>' vs '!array.type<5 x !felt.type>'}}
    %v = array.read %arr[%k] : !array.type<@Unknown x !felt.type>, !felt.type
    function.return
  }
}
// -----
module attributes {llzk.lang} {
  function.def @ReadArrayOp_unknown_result_tvar(%arr: !array.type<5 x !felt.type>, %k: index) {
    // expected-error@+1 {{'array.read' op references unknown symbol "@Unknown"}}
    %v = array.read %arr[%k] : !array.type<5 x !felt.type>, !poly.tvar<@Unknown>
    function.return
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @ReadArrayOpWithUnknownInResultTypeArrElem<[@T]> {
    function.def @compute(%arr: !array.type<5 x !poly.tvar<@T>>) -> !struct.type<@ReadArrayOpWithUnknownInResultTypeArrElem<[@T]>> {
      %self = struct.new : !struct.type<@ReadArrayOpWithUnknownInResultTypeArrElem<[@T]>>
      function.return %self : !struct.type<@ReadArrayOpWithUnknownInResultTypeArrElem<[@T]>>
    }
    function.def @constrain(%self: !struct.type<@ReadArrayOpWithUnknownInResultTypeArrElem<[@T]>>, %arr: !array.type<5 x !poly.tvar<@T>>) {
      %k = arith.constant 0 : index
      // expected-error@+2 {{'array.read' op references unknown symbol "@Unknown"}}
      // expected-error@+1 {{could not find 'struct.def' named "@Unknown"}}
      %v = array.read %arr[%k] : !array.type<5 x !poly.tvar<@T>>, !struct.type<@Unknown>
      function.return
    }
  }
}
// -----
module attributes {llzk.lang} {
  function.def @WriteArrayOp_unknown_base(
      %arr: !array.type<5 x !felt.type>, // expected-note {{prior use here}}
      %k: index,
      %v: !felt.type
  ) {
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%arr' expects different type than prior uses: '!array.type<@Unknown x !felt.type>' vs '!array.type<5 x !felt.type>'}}
    array.write %arr[%k] = %v : !array.type<@Unknown x !felt.type>, !felt.type
    function.return
  }
}
// -----
module attributes {llzk.lang} {
  function.def @WriteArrayOp_unknown_result_tvar(
      %arr: !array.type<5 x !felt.type>,
      %k: index,
      %v: !felt.type // expected-note {{prior use here}}
  ) {
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%v' expects different type than prior uses: '!poly.tvar<@Unknown>' vs '!felt.type'}}
    array.write %arr[%k] = %v : !array.type<5 x !felt.type>, !poly.tvar<@Unknown>
    function.return
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @WriteArrayOpWithUnknownInResultTypeArrElem<[@T]> {
    function.def @compute(%arr: !array.type<5 x !poly.tvar<@T>>,
      %v: !struct.type<@T> // expected-note {{prior use here}}
    ) -> !struct.type<@WriteArrayOpWithUnknownInResultTypeArrElem<[@T]>> {
      %self = struct.new : !struct.type<@WriteArrayOpWithUnknownInResultTypeArrElem<[@T]>>
      %k = arith.constant 0 : index
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%v' expects different type than prior uses: '!struct.type<@Unknown>' vs '!struct.type<@T>'}}
      array.write %arr[%k] = %v : !array.type<5 x !poly.tvar<@T>>, !struct.type<@Unknown>
      function.return %self : !struct.type<@WriteArrayOpWithUnknownInResultTypeArrElem<[@T]>>
    }
    function.def @constrain(%self: !struct.type<@WriteArrayOpWithUnknownInResultTypeArrElem<[@T]>>,
        %arr: !array.type<5 x !poly.tvar<@T>>,
        %v: !struct.type<@T>
    ) {
      %k = arith.constant 0 : index
      function.return
    }
  }
}
// -----
module attributes {llzk.lang} {
  function.def private @array_extract_with_unknown_base(
      %r: !array.type<2,2 x i1> // expected-note {{prior use here}}
  ) {
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%r' expects different type than prior uses: '!array.type<2,2 x !poly.tvar<@Unknown>>' vs '!array.type<2,2 x i1>'}}
    %s = array.extract %r[] : !array.type<2,2 x !poly.tvar<@Unknown>>
    function.return
  }
}
// -----
module attributes {llzk.lang} {
  function.def @array_len_with_unknown_base(
      %a: !array.type<2,3 x !felt.type>, // expected-note {{prior use here}}
      %0: index
  ) -> index {
    // See NOTE on line 4.
    // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!array.type<2,@Unknown x !felt.type>' vs '!array.type<2,3 x !felt.type>'}}
    %x = array.len %a, %0 : !array.type<2,@Unknown x !felt.type>
    function.return %x: index
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @EmitEqOp_With_Unknown_In_LHS {
    function.def @constrain(%self: !struct.type<@EmitEqOp_With_Unknown_In_LHS>) {
      %a = array.new : !array.type<5 x !felt.type> // expected-note {{prior use here}}
      %b = array.new : !array.type<5 x !felt.type>
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!array.type<@Unknown x !felt.type>' vs '!array.type<5 x !felt.type>'}}
      constrain.eq %a, %b : !array.type<@Unknown x !felt.type>
      function.return
    }

    function.def @compute() -> !struct.type<@EmitEqOp_With_Unknown_In_LHS> {
      %self = struct.new : !struct.type<@EmitEqOp_With_Unknown_In_LHS>
      function.return %self : !struct.type<@EmitEqOp_With_Unknown_In_LHS>
    }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @EmitEqOp_With_Unknown_In_RHS {
    function.def @constrain(%self: !struct.type<@EmitEqOp_With_Unknown_In_RHS>) {
      %a = array.new : !array.type<5 x !felt.type>
      %b = array.new : !array.type<5 x !felt.type> // expected-note {{prior use here}}
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%b' expects different type than prior uses: '!array.type<@Unknown x !felt.type>' vs '!array.type<5 x !felt.type>'}}
      constrain.eq %a, %b : !array.type<5 x !felt.type>, !array.type<@Unknown x !felt.type>
      function.return
    }

    function.def @compute() -> !struct.type<@EmitEqOp_With_Unknown_In_RHS> {
      %self = struct.new : !struct.type<@EmitEqOp_With_Unknown_In_RHS>
      function.return %self : !struct.type<@EmitEqOp_With_Unknown_In_RHS>
    }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @EmitInOp_With_Unknown_In_LHS {
    function.def @constrain(%self: !struct.type<@EmitInOp_With_Unknown_In_LHS>) {
      %a = array.new : !array.type<5 x !felt.type> // expected-note {{prior use here}}
      %b = felt.const 3
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!array.type<@Unknown x !felt.type>' vs '!array.type<5 x !felt.type>'}}
      constrain.in %a, %b : !array.type<@Unknown x !felt.type>, !felt.type
      function.return
    }

    function.def @compute() -> !struct.type<@EmitInOp_With_Unknown_In_LHS> {
      %self = struct.new : !struct.type<@EmitInOp_With_Unknown_In_LHS>
      function.return %self : !struct.type<@EmitInOp_With_Unknown_In_LHS>
    }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @EmitInOp_With_Unknown_In_RHS {
    function.def @constrain(%self: !struct.type<@EmitInOp_With_Unknown_In_RHS>) {
      %a = array.new : !array.type<5 x !felt.type>
      %b = felt.const 3 // expected-note {{prior use here}}
      // See NOTE on line 4.
      // expected-error@+1 {{use of value '%b' expects different type than prior uses: '!poly.tvar<@Unknown>' vs '!felt.type'}}
      constrain.in %a, %b : !array.type<5 x !felt.type>, !poly.tvar<@Unknown>
      function.return
    }

    function.def @compute() -> !struct.type<@EmitInOp_With_Unknown_In_RHS> {
      %self = struct.new : !struct.type<@EmitInOp_With_Unknown_In_RHS>
      function.return %self : !struct.type<@EmitInOp_With_Unknown_In_RHS>
    }
  }
}
