// RUN: llzk-opt -split-input-file -verify-diagnostics %s

module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentB {
    function.def @compute() -> !struct.type<@ComponentB> {
      %self = struct.new : !struct.type<@ComponentB>
      function.return %self : !struct.type<@ComponentB>
    }

    function.def @constrain(%p: !struct.type<@ComponentB>) {
      // expected-error@+2 {{'struct.readm' op references unknown symbol "@f2"}}
      // expected-error@+1 {{could not find 'struct.member' named "@f2" in "@ComponentB"}}
      %0 = struct.readm %p[@f2] : !struct.type<@ComponentB>, !felt.type
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentC {
    struct.member @f2 : index

    function.def @compute() -> !struct.type<@ComponentC> {
      %self = struct.new : !struct.type<@ComponentC>
      function.return %self : !struct.type<@ComponentC>
    }

    function.def @constrain(%p: !struct.type<@ComponentC>) {
      // expected-error@+1 {{'struct.readm' op has wrong type; expected 'index', got '!felt.type}}
      %0 = struct.readm %p[@f2] : !struct.type<@ComponentC>, !felt.type
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentD {
    struct.member @f2 : index

    function.def @compute() -> !struct.type<@ComponentD> {
      %self = struct.new : !struct.type<@ComponentD>
      function.return %self : !struct.type<@ComponentD>
    }

    function.def @constrain(%p: !struct.type<@ComponentD>) { // expected-note {{prior use here}}
      // expected-error@+1 {{use of value '%p' expects different type than prior uses: '!struct.type<@WrongComponent>' vs '!struct.type<@ComponentD>'}}
      %0 = struct.readm %p[@f2] : !struct.type<@WrongComponent>, index
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentF {
    struct.member @f2 : index

    function.def @compute() -> !struct.type<@ComponentF> {
      %self = struct.new : !struct.type<@ComponentF>
      function.return %self : !struct.type<@ComponentF>
    }

    function.def @constrain(%p: !struct.type<@WrongComponent>) { // expected-note {{prior use here}}
      // expected-error@+1 {{use of value '%p' expects different type than prior uses: '!struct.type<@ComponentF>' vs '!struct.type<@WrongComponent>'}}
      %0 = struct.readm %p[@f2] : !struct.type<@ComponentF>, index
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentH {
    function.def @compute(%0: !felt.type) -> !struct.type<@ComponentH> {
      %self = struct.new : !struct.type<@ComponentH>
      // expected-error@+2 {{'struct.writem' op references unknown symbol "@f2"}}
      // expected-error@+1 {{could not find 'struct.member' named "@f2" in "@ComponentH"}}
      struct.writem %self[@f2] = %0 : !struct.type<@ComponentH>, !felt.type
      function.return %self : !struct.type<@ComponentH>
    }

    function.def @constrain(%self: !struct.type<@ComponentH>, %0: !felt.type) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentJ {
    struct.member @f2 : index

    function.def @compute(%0: !felt.type) -> !struct.type<@ComponentJ> {
      %self = struct.new : !struct.type<@ComponentJ>
      // expected-error@+1 {{'struct.writem' op has wrong type; expected 'index', got '!felt.type}}
      struct.writem %self[@f2] = %0 : !struct.type<@ComponentJ>, !felt.type
      function.return %self : !struct.type<@ComponentJ>
    }

    function.def @constrain(%self: !struct.type<@ComponentJ>, %0: !felt.type) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentK {
    struct.member @f2 : index

    function.def @compute() -> !struct.type<@ComponentK> {
      %self = struct.new : !struct.type<@ComponentK>
      function.return %self : !struct.type<@ComponentK>
    }

    function.def @constrain(%p: !struct.type<@ComponentK>, %0: !felt.type) { // expected-note {{prior use here}}
      // expected-error@+1 {{use of value '%p' expects different type than prior uses: '!struct.type<@WrongComponent>' vs '!struct.type<@ComponentK>'}}
      struct.writem %p[@f2] = %0 : !struct.type<@WrongComponent>, index
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentL {
    struct.member @f2 : index

    function.def @compute() -> !struct.type<@ComponentL> {
      %self = struct.new : !struct.type<@ComponentL>
      function.return %self : !struct.type<@ComponentL>
    }

    function.def @constrain(%p: !struct.type<@WrongComponent>, %0: !felt.type) { // expected-note {{prior use here}}
      // expected-error@+1 {{use of value '%p' expects different type than prior uses: '!struct.type<@ComponentL>' vs '!struct.type<@WrongComponent>'}}
      struct.writem %p[@f2] = %0 : !struct.type<@ComponentL>, index
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentM {
    struct.member @f2 : !array.type<2 x index>

    function.def @compute() -> !struct.type<@ComponentM> {
      %self = struct.new : !struct.type<@ComponentM>
      function.return %self : !struct.type<@ComponentM>
    }

    function.def @constrain(%p: !struct.type<@ComponentM>, %0: !array.type<7 x index>) { // expected-note {{prior use here}}
      // expected-error@+1 {{use of value '%0' expects different type than prior uses: '!array.type<2 x index>' vs '!array.type<7 x index>'}}
      struct.writem %p[@f2] = %0 : !struct.type<@ComponentM>, !array.type<2 x index>
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @WrongComponent { // expected-note {{uses this type instead}}
    struct.member @f : !felt.type
    function.def @compute() -> !struct.type<@WrongComponent> {
      %self = struct.new : !struct.type<@WrongComponent>
      function.return %self : !struct.type<@WrongComponent>
    }
    function.def @constrain(%self: !struct.type<@WrongComponent>)  { function.return }
  }

  struct.def @ComponentN {
    function.def @compute(%0: !struct.type<@WrongComponent>, %1: !felt.type) -> !struct.type<@ComponentN> {
      %self = struct.new : !struct.type<@ComponentN>
      // expected-error@+1 {{'struct.writem' op must use type of its ancestor 'struct.def' "@ComponentN" as base value type}}
      struct.writem %0[@f] = %1 : !struct.type<@WrongComponent>, !felt.type
      function.return %self : !struct.type<@ComponentN>
    }
    function.def @constrain(%self: !struct.type<@ComponentN>, %0: !struct.type<@WrongComponent>, %1: !felt.type)  { function.return }
  }
}

// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @PrivateComponent {
    struct.member @f : !felt.type // expected-note {{member defined here}}
    function.def @compute() -> !struct.type<@PrivateComponent> {
      %self = struct.new : !struct.type<@PrivateComponent>
      function.return %self : !struct.type<@PrivateComponent>
    }
    function.def @constrain(%self: !struct.type<@PrivateComponent>)  { function.return }
  }

  struct.def @ComponentN {
    function.def @compute(%0: !struct.type<@PrivateComponent>, %1: !felt.type) -> !struct.type<@ComponentN> {
      %self = struct.new : !struct.type<@ComponentN>
      // expected-error@+1 {{'struct.readm' op cannot read from private member of struct "@PrivateComponent"}}
      %res = struct.readm %0[@f] : !struct.type<@PrivateComponent>, !felt.type
      function.return %self : !struct.type<@ComponentN>
    }
    function.def @constrain(%self: !struct.type<@ComponentN>, %0: !struct.type<@PrivateComponent>, %1: !felt.type)  { function.return }
  }
}
