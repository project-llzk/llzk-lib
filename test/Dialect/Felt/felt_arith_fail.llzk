// RUN: llzk-opt -split-input-file -verify-diagnostics %s

function.def @f_neg(
    %a: !felt.type // expected-note {{prior use here}}
) -> !felt.type {
  // expected-error@+1 {{use of value '%a' expects different type than prior uses: 'index' vs '!felt.type'}}
  %c = felt.neg %a : index
  function.return %c: !felt.type
}
// -----

function.def @f_inv(
    %a: index
) -> index attributes {function.allow_non_native_field_ops} {
  // expected-error@+1 {{'felt.inv' op operand #0 must be finite field element or type variable, but got 'index'}}
  %c = felt.inv %a : index
  function.return %c: index
}
// -----

function.def @f_sub(
    %a: !felt.type,
    %b: !felt.type // expected-note {{prior use here}}
) -> !felt.type {
  // expected-error@+1 {{use of value '%b' expects different type than prior uses: 'i1' vs '!felt.type'}}
  %c = felt.sub %a, %b : !felt.type, i1
  function.return %c: !felt.type
}
// -----

function.def @f_add(
    %a: !felt.type,
    %b: !array.type<5 x i1>
) -> !felt.type {
  // expected-error@+1 {{'felt.add' op operand #1 must be finite field element or type variable, but got '!array.type<5 x i1>'}}
  %c = felt.add %a, %b : !felt.type, !array.type<5 x i1>
  function.return %c: !felt.type
}

// -----

function.def @f_const_bad_field()  {
  // expected-error@+1 {{custom op 'felt.const' field 'moo' is not defined}}
  %c = felt.const 42 <"moo">
  function.return
}

// -----

// expected-error@+1 {{field 'moo' is not defined}}
function.def @f_add_bad_field(%a: !felt.type<"moo">, %b: !felt.type<"moo">)  {
  %c = felt.add %a, %b : !felt.type<"moo">, !felt.type<"moo">
  function.return
}
