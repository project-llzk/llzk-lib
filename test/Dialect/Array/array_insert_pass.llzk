// RUN: llzk-opt -split-input-file %s 2>&1 | FileCheck --enable-var-scope %s

function.def @constant_index() {
  %a = arith.constant 1 : index
  %b = arith.constant 2 : index
  %c = arith.constant 3 : index
  %d = arith.constant 4 : index
  %1 = array.new %a, %b, %c, %d : !array.type<2,2 x index>
  // %1 = [ [1, 2], [3, 4] ]
  %x = arith.constant 9 : index
  %y = arith.constant 8 : index

  %i = arith.constant 0 : index
  %2 = array.new %x, %y : !array.type<2 x index>
  array.insert %1[%i] = %2 : !array.type<2,2 x index>, !array.type<2 x index>
  // %1 = [ [9, 8], [3, 4] ]

  %j = arith.constant 1 : index
  %3 = array.new %y, %x : !array.type<2 x index>
  array.insert %1[%j] = %3 : !array.type<2,2 x index>, !array.type<2 x index>
  // %1 = [ [9, 8], [8, 9] ]

  function.return
}
// CHECK-LABEL: function.def @constant_index() {
// CHECK-NEXT:    %[[VAL_0:[0-9a-zA-Z_\.]+]] = arith.constant 1 : index
// CHECK-NEXT:    %[[VAL_1:[0-9a-zA-Z_\.]+]] = arith.constant 2 : index
// CHECK-NEXT:    %[[VAL_2:[0-9a-zA-Z_\.]+]] = arith.constant 3 : index
// CHECK-NEXT:    %[[VAL_3:[0-9a-zA-Z_\.]+]] = arith.constant 4 : index
// CHECK-NEXT:    %[[VAL_4:[0-9a-zA-Z_\.]+]] = array.new %[[VAL_0]], %[[VAL_1]], %[[VAL_2]], %[[VAL_3]] : <2,2 x index>
// CHECK-NEXT:    %[[VAL_5:[0-9a-zA-Z_\.]+]] = arith.constant 9 : index
// CHECK-NEXT:    %[[VAL_6:[0-9a-zA-Z_\.]+]] = arith.constant 8 : index
// CHECK-NEXT:    %[[VAL_7:[0-9a-zA-Z_\.]+]] = arith.constant 0 : index
// CHECK-NEXT:    %[[VAL_8:[0-9a-zA-Z_\.]+]] = array.new %[[VAL_5]], %[[VAL_6]] : <2 x index>
// CHECK-NEXT:    array.insert %[[VAL_4]]{{\[}}%[[VAL_7]]] = %[[VAL_8]] : <2,2 x index>, <2 x index>
// CHECK-NEXT:    %[[VAL_9:[0-9a-zA-Z_\.]+]] = arith.constant 1 : index
// CHECK-NEXT:    %[[VAL_10:[0-9a-zA-Z_\.]+]] = array.new %[[VAL_6]], %[[VAL_5]] : <2 x index>
// CHECK-NEXT:    array.insert %[[VAL_4]]{{\[}}%[[VAL_9]]] = %[[VAL_10]] : <2,2 x index>, <2 x index>
// CHECK-NEXT:    function.return
// CHECK-NEXT:  }
// -----

function.def @empty_index(%0: !felt.type, %1: !felt.type, %z: !array.type<84,22,67 x !felt.type>) {
  %r = array.new : !array.type<84,22,67 x !felt.type>
  array.insert %r[] = %z : !array.type<84,22,67 x !felt.type>, !array.type<84,22,67 x !felt.type>
  // %r is completely overwritten with contents of %z
  function.return
}
// CHECK-LABEL: function.def @empty_index(
// CHECK-SAME:                         %[[VAL_0:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:                         %[[VAL_1:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:                         %[[VAL_2:[0-9a-zA-Z_\.]+]]: !array.type<84,22,67 x !felt.type>) {
// CHECK-NEXT:    %[[VAL_3:[0-9a-zA-Z_\.]+]] = array.new  : <84,22,67 x !felt.type>
// CHECK-NEXT:    array.insert %[[VAL_3]][] = %[[VAL_2]] : <84,22,67 x !felt.type>, <84,22,67 x !felt.type>
// CHECK-NEXT:    function.return
// CHECK-NEXT:  }
// -----

function.def @trival(%z: !array.type<0 x index>) {
  %r = array.new : !array.type<0 x index>
  array.insert %r[] = %z : !array.type<0 x index>, !array.type<0 x index>
  function.return
}
// CHECK-LABEL: function.def @trival(
// CHECK-SAME:                    %[[VAL_0:[0-9a-zA-Z_\.]+]]: !array.type<0 x index>) {
// CHECK-NEXT:    %[[VAL_1:[0-9a-zA-Z_\.]+]] = array.new  : <0 x index>
// CHECK-NEXT:    array.insert %[[VAL_1]][] = %[[VAL_0]] : <0 x index>, <0 x index>
// CHECK-NEXT:    function.return
// CHECK-NEXT:  }
// -----

function.def @doc_example(%r: i1, %s: i1, %t: i1, %x: i1, %y: i1, %z: i1) {
  %c = array.new : !array.type<2,3 x i1>
  // Array %c is uninitialized [[?, ?, ?], [?, ?, ?]]
  %0 = arith.constant 0 : index
  %a = array.new %r, %s, %t : !array.type<3 x i1>
  array.insert %c[%0] = %a : !array.type<2,3 x i1>, !array.type<3 x i1>
  // Array %c is now [[%r, %s, %t], [?, ?, ?]]
  %1 = arith.constant 1 : index
  %b = array.new %x, %y, %z : !array.type<3 x i1>
  array.insert %c[%1] = %b : !array.type<2,3 x i1>, !array.type<3 x i1>
  // Array %c is now [[%r, %s, %t], [%x, %y, %z]]
  function.return
}
// CHECK-LABEL: function.def @doc_example(
// CHECK-SAME:                         %[[VAL_0:[0-9a-zA-Z_\.]+]]: i1,
// CHECK-SAME:                         %[[VAL_1:[0-9a-zA-Z_\.]+]]: i1,
// CHECK-SAME:                         %[[VAL_2:[0-9a-zA-Z_\.]+]]: i1,
// CHECK-SAME:                         %[[VAL_3:[0-9a-zA-Z_\.]+]]: i1,
// CHECK-SAME:                         %[[VAL_4:[0-9a-zA-Z_\.]+]]: i1,
// CHECK-SAME:                         %[[VAL_5:[0-9a-zA-Z_\.]+]]: i1) {
// CHECK-NEXT:    %[[VAL_6:[0-9a-zA-Z_\.]+]] = array.new  : <2,3 x i1>
// CHECK-NEXT:    %[[VAL_7:[0-9a-zA-Z_\.]+]] = arith.constant 0 : index
// CHECK-NEXT:    %[[VAL_8:[0-9a-zA-Z_\.]+]] = array.new %[[VAL_0]], %[[VAL_1]], %[[VAL_2]] : <3 x i1>
// CHECK-NEXT:    array.insert %[[VAL_6]]{{\[}}%[[VAL_7]]] = %[[VAL_8]] : <2,3 x i1>, <3 x i1>
// CHECK-NEXT:    %[[VAL_9:[0-9a-zA-Z_\.]+]] = arith.constant 1 : index
// CHECK-NEXT:    %[[VAL_10:[0-9a-zA-Z_\.]+]] = array.new %[[VAL_3]], %[[VAL_4]], %[[VAL_5]] : <3 x i1>
// CHECK-NEXT:    array.insert %[[VAL_6]]{{\[}}%[[VAL_9]]] = %[[VAL_10]] : <2,3 x i1>, <3 x i1>
// CHECK-NEXT:    function.return
// CHECK-NEXT:  }
// -----

function.def @more_dimensions(%a: i1, %b: i1, %c: i1, %d: i1, %e: i1, %f: i1) {
  %a0 = array.new : !array.type<2,3,2,3 x i1>

  %0 = arith.constant 0 : index
  %1 = arith.constant 1 : index
  %2 = arith.constant 2 : index

  %a1 = array.new %a, %b, %c, %d, %e, %f : !array.type<2,3 x i1>
  array.insert %a0[%0, %0] = %a1 : !array.type<2,3,2,3 x i1>, !array.type<2,3 x i1>
  %a2 = array.new %c, %d, %e, %f, %a, %b : !array.type<2,3 x i1>
  array.insert %a0[%0, %1] = %a2 : !array.type<2,3,2,3 x i1>, !array.type<2,3 x i1>
  %a3 = array.new %e, %f, %a, %b, %c, %d : !array.type<2,3 x i1>
  array.insert %a0[%0, %2] = %a3 : !array.type<2,3,2,3 x i1>, !array.type<2,3 x i1>

  %b0 = array.new : !array.type<3,2,3 x i1>

  %b1 = array.new %a, %b, %c, %d, %e, %f : !array.type<2,3 x i1>
  array.insert %b0[%0] = %b1 : !array.type<3,2,3 x i1>, !array.type<2,3 x i1>
  %b2 = array.new %c, %d, %e, %f, %a, %b : !array.type<2,3 x i1>
  array.insert %b0[%1] = %b2 : !array.type<3,2,3 x i1>, !array.type<2,3 x i1>
  %b3 = array.new %e, %f, %a, %b, %c, %d : !array.type<2,3 x i1>
  array.insert %b0[%2] = %b3 : !array.type<3,2,3 x i1>, !array.type<2,3 x i1>

  array.insert %a0[%1] = %b0 : !array.type<2,3,2,3 x i1>, !array.type<3,2,3 x i1>

  function.return
}
// CHECK-LABEL: function.def @more_dimensions(
// CHECK-SAME:                             %[[V_0:[0-9a-zA-Z_\.]+]]: i1,
// CHECK-SAME:                             %[[V_1:[0-9a-zA-Z_\.]+]]: i1,
// CHECK-SAME:                             %[[V_2:[0-9a-zA-Z_\.]+]]: i1,
// CHECK-SAME:                             %[[V_3:[0-9a-zA-Z_\.]+]]: i1,
// CHECK-SAME:                             %[[V_4:[0-9a-zA-Z_\.]+]]: i1,
// CHECK-SAME:                             %[[V_5:[0-9a-zA-Z_\.]+]]: i1) {
// CHECK-NEXT:    %[[V_6:[0-9a-zA-Z_\.]+]] = array.new  : <2,3,2,3 x i1>
// CHECK-NEXT:    %[[V_7:[0-9a-zA-Z_\.]+]] = arith.constant 0 : index
// CHECK-NEXT:    %[[V_8:[0-9a-zA-Z_\.]+]] = arith.constant 1 : index
// CHECK-NEXT:    %[[V_9:[0-9a-zA-Z_\.]+]] = arith.constant 2 : index
// CHECK-NEXT:    %[[V_10:[0-9a-zA-Z_\.]+]] = array.new %[[V_0]], %[[V_1]], %[[V_2]], %[[V_3]], %[[V_4]], %[[V_5]] : <2,3 x i1>
// CHECK-NEXT:    array.insert %[[V_6]]{{\[}}%[[V_7]], %[[V_7]]] = %[[V_10]] : <2,3,2,3 x i1>, <2,3 x i1>
// CHECK-NEXT:    %[[V_11:[0-9a-zA-Z_\.]+]] = array.new %[[V_2]], %[[V_3]], %[[V_4]], %[[V_5]], %[[V_0]], %[[V_1]] : <2,3 x i1>
// CHECK-NEXT:    array.insert %[[V_6]]{{\[}}%[[V_7]], %[[V_8]]] = %[[V_11]] : <2,3,2,3 x i1>, <2,3 x i1>
// CHECK-NEXT:    %[[V_12:[0-9a-zA-Z_\.]+]] = array.new %[[V_4]], %[[V_5]], %[[V_0]], %[[V_1]], %[[V_2]], %[[V_3]] : <2,3 x i1>
// CHECK-NEXT:    array.insert %[[V_6]]{{\[}}%[[V_7]], %[[V_9]]] = %[[V_12]] : <2,3,2,3 x i1>, <2,3 x i1>
// CHECK-NEXT:    %[[V_13:[0-9a-zA-Z_\.]+]] = array.new  : <3,2,3 x i1>
// CHECK-NEXT:    %[[V_14:[0-9a-zA-Z_\.]+]] = array.new %[[V_0]], %[[V_1]], %[[V_2]], %[[V_3]], %[[V_4]], %[[V_5]] : <2,3 x i1>
// CHECK-NEXT:    array.insert %[[V_13]]{{\[}}%[[V_7]]] = %[[V_14]] : <3,2,3 x i1>, <2,3 x i1>
// CHECK-NEXT:    %[[V_15:[0-9a-zA-Z_\.]+]] = array.new %[[V_2]], %[[V_3]], %[[V_4]], %[[V_5]], %[[V_0]], %[[V_1]] : <2,3 x i1>
// CHECK-NEXT:    array.insert %[[V_13]]{{\[}}%[[V_8]]] = %[[V_15]] : <3,2,3 x i1>, <2,3 x i1>
// CHECK-NEXT:    %[[V_16:[0-9a-zA-Z_\.]+]] = array.new %[[V_4]], %[[V_5]], %[[V_0]], %[[V_1]], %[[V_2]], %[[V_3]] : <2,3 x i1>
// CHECK-NEXT:    array.insert %[[V_13]]{{\[}}%[[V_9]]] = %[[V_16]] : <3,2,3 x i1>, <2,3 x i1>
// CHECK-NEXT:    array.insert %[[V_6]]{{\[}}%[[V_8]]] = %[[V_13]] : <2,3,2,3 x i1>, <3,2,3 x i1>
// CHECK-NEXT:    function.return
// CHECK-NEXT:  }
// -----

module attributes {llzk.lang} {
  struct.def @UnificationInDims<[@A, @B]> {
    function.def @compute(%a: !array.type<@A x i1>, %b: !array.type<@B x i1>) -> !struct.type<@UnificationInDims<[@A, @B]>> {
      %self = struct.new : !struct.type<@UnificationInDims<[@A, @B]>>
      %c = array.new : !array.type<2,3 x i1>
      %0 = arith.constant 0 : index
      array.insert %c[%0] = %a : !array.type<2,3 x i1>, !array.type<@A x i1>
      %1 = arith.constant 1 : index
      array.insert %c[%1] = %b : !array.type<2,3 x i1>, !array.type<@B x i1>
      function.return %self : !struct.type<@UnificationInDims<[@A, @B]>>
    }
    function.def @constrain(%self: !struct.type<@UnificationInDims<[@A, @B]>>, %a: !array.type<@A x i1>, %b: !array.type<@B x i1>) { function.return }
  }
}
// CHECK-LABEL: struct.def @UnificationInDims<[@A, @B]> {
// CHECK-NEXT:    function.def @compute(
// CHECK-SAME:                  %[[V_0:[0-9a-zA-Z_\.]+]]: !array.type<@A x i1>,
// CHECK-SAME:                  %[[V_1:[0-9a-zA-Z_\.]+]]: !array.type<@B x i1>
// CHECK-SAME:                  ) -> !struct.type<@UnificationInDims<[@A, @B]>> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V_2:[0-9a-zA-Z_\.]+]] = struct.new : <@UnificationInDims<[@A, @B]>>
// CHECK-NEXT:      %[[V_3:[0-9a-zA-Z_\.]+]] = array.new  : <2,3 x i1>
// CHECK-NEXT:      %[[V_4:[0-9a-zA-Z_\.]+]] = arith.constant 0 : index
// CHECK-NEXT:      array.insert %[[V_3]]{{\[}}%[[V_4]]] = %[[V_0]] : <2,3 x i1>, <@A x i1>
// CHECK-NEXT:      %[[V_5:[0-9a-zA-Z_\.]+]] = arith.constant 1 : index
// CHECK-NEXT:      array.insert %[[V_3]]{{\[}}%[[V_5]]] = %[[V_1]] : <2,3 x i1>, <@B x i1>
// CHECK-NEXT:      function.return %[[V_2]] : !struct.type<@UnificationInDims<[@A, @B]>>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(
// CHECK-SAME:                    %[[V_6:[0-9a-zA-Z_\.]+]]: !struct.type<@UnificationInDims<[@A, @B]>>,
// CHECK-SAME:                    %[[V_7:[0-9a-zA-Z_\.]+]]: !array.type<@A x i1>,
// CHECK-SAME:                    %[[V_8:[0-9a-zA-Z_\.]+]]: !array.type<@B x i1>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
// -----

module attributes {llzk.lang} {
  struct.def @UnificationInTypes<[@A, @B]> {
    function.def @compute(%a: !array.type<3 x !poly.tvar<@A>>, %b: !array.type<3 x !poly.tvar<@B>>) -> !struct.type<@UnificationInTypes<[@A, @B]>> {
      %self = struct.new : !struct.type<@UnificationInTypes<[@A, @B]>>
      %c = array.new : !array.type<2,3 x i1>
      %0 = arith.constant 0 : index
      array.insert %c[%0] = %a : !array.type<2,3 x i1>, !array.type<3 x !poly.tvar<@A>>
      %1 = arith.constant 1 : index
      array.insert %c[%1] = %b : !array.type<2,3 x i1>, !array.type<3 x !poly.tvar<@B>>
      function.return %self : !struct.type<@UnificationInTypes<[@A, @B]>>
    }
    function.def @constrain(%self: !struct.type<@UnificationInTypes<[@A, @B]>>, %a: !array.type<3 x !poly.tvar<@A>>, %b: !array.type<3 x !poly.tvar<@B>>) { function.return }
  }
}
// CHECK-LABEL: struct.def @UnificationInTypes<[@A, @B]> {
// CHECK-NEXT:    function.def @compute(
// CHECK-SAME:                  %[[V_0:[0-9a-zA-Z_\.]+]]: !array.type<3 x !poly.tvar<@A>>,
// CHECK-SAME:                  %[[V_1:[0-9a-zA-Z_\.]+]]: !array.type<3 x !poly.tvar<@B>>
// CHECK-SAME:                  ) -> !struct.type<@UnificationInTypes<[@A, @B]>> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V_2:[0-9a-zA-Z_\.]+]] = struct.new : <@UnificationInTypes<[@A, @B]>>
// CHECK-NEXT:      %[[V_3:[0-9a-zA-Z_\.]+]] = array.new  : <2,3 x i1>
// CHECK-NEXT:      %[[V_4:[0-9a-zA-Z_\.]+]] = arith.constant 0 : index
// CHECK-NEXT:      array.insert %[[V_3]]{{\[}}%[[V_4]]] = %[[V_0]] : <2,3 x i1>, <3 x !poly.tvar<@A>>
// CHECK-NEXT:      %[[V_5:[0-9a-zA-Z_\.]+]] = arith.constant 1 : index
// CHECK-NEXT:      array.insert %[[V_3]]{{\[}}%[[V_5]]] = %[[V_1]] : <2,3 x i1>, <3 x !poly.tvar<@B>>
// CHECK-NEXT:      function.return %[[V_2]] : !struct.type<@UnificationInTypes<[@A, @B]>>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(
// CHECK-SAME:                    %[[V_6:[0-9a-zA-Z_\.]+]]: !struct.type<@UnificationInTypes<[@A, @B]>>,
// CHECK-SAME:                    %[[V_7:[0-9a-zA-Z_\.]+]]: !array.type<3 x !poly.tvar<@A>>,
// CHECK-SAME:                    %[[V_8:[0-9a-zA-Z_\.]+]]: !array.type<3 x !poly.tvar<@B>>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
// -----

#map = affine_map<(i)[] -> (i)>
function.def @dimensions_unify_affine_with_int_A(%z: !array.type<#map x !felt.type>) {
  %r = array.new : !array.type<44,2 x !felt.type>
  %0 = arith.constant 0 : index
  array.insert %r[%0] = %z : !array.type<44,2 x !felt.type>, !array.type<#map x !felt.type>
  function.return
}
// CHECK: #[[$M0:[0-9a-zA-Z_\.]+]] = affine_map<(d0) -> (d0)>
// CHECK-LABEL: function.def @dimensions_unify_affine_with_int_A(
// CHECK-SAME:              %[[V0:[0-9a-zA-Z_\.]+]]: !array.type<#[[$M0]] x !felt.type>) {
// CHECK-NEXT:    %[[V1:[0-9a-zA-Z_\.]+]] = array.new  : <44,2 x !felt.type>
// CHECK-NEXT:    %[[V2:[0-9a-zA-Z_\.]+]] = arith.constant 0 : index
// CHECK-NEXT:    array.insert %[[V1]]{{\[}}%[[V2]]] = %[[V0]] : <44,2 x !felt.type>, <#[[$M0]] x !felt.type>
// CHECK-NEXT:    function.return
// CHECK-NEXT:  }
// -----

#map = affine_map<()[j] -> (j)>
function.def @dimensions_unify_affine_with_int_B(%z: !array.type<#map x !felt.type>) {
  %r = array.new : !array.type<44,2 x !felt.type>
  %0 = arith.constant 0 : index
  array.insert %r[%0] = %z : !array.type<44,2 x !felt.type>, !array.type<#map x !felt.type>
  function.return
}
// CHECK: #[[$M0:[0-9a-zA-Z_\.]+]] = affine_map<()[s0] -> (s0)>
// CHECK-LABEL: function.def @dimensions_unify_affine_with_int_B(
// CHECK-SAME:              %[[V0:[0-9a-zA-Z_\.]+]]: !array.type<#[[$M0]] x !felt.type>) {
// CHECK-NEXT:    %[[V1:[0-9a-zA-Z_\.]+]] = array.new  : <44,2 x !felt.type>
// CHECK-NEXT:    %[[V2:[0-9a-zA-Z_\.]+]] = arith.constant 0 : index
// CHECK-NEXT:    array.insert %[[V1]]{{\[}}%[[V2]]] = %[[V0]] : <44,2 x !felt.type>, <#[[$M0]] x !felt.type>
// CHECK-NEXT:    function.return
// CHECK-NEXT:  }
