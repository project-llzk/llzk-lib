// RUN: llzk-opt -split-input-file -verify-diagnostics %s

// TESTS: invalid type for a global
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  global.def @g : !poly.tvar<@X>
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  global.def @g : i64
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  global.def @g : !array.type<@b x i1>
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  global.def @g : !array.type<affine_map<(i)->(i)> x i1>
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  global.def @g : !struct.type<@Component01<[affine_map<(i)->(i)>]>>
  struct.def @Component01<[@N]> {
    function.def @compute() -> !struct.type<@Component01<[@N]>> {
      %self = struct.new : !struct.type<@Component01<[@N]>>
      function.return %self : !struct.type<@Component01<[@N]>>
    }
    function.def @constrain(%self: !struct.type<@Component01<[@N]>>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op attribute 'type' failed to satisfy constraint: type attribute of any LLZK type except non-constant types}}
  global.def @g : !struct.type<@Component01<[@any_symbol_here_is_illegal]>>
  struct.def @Component01<[@N]> {
    function.def @compute() -> !struct.type<@Component01<[@N]>> {
      %self = struct.new : !struct.type<@Component01<[@N]>>
      function.return %self : !struct.type<@Component01<[@N]>>
    }
    function.def @constrain(%self: !struct.type<@Component01<[@N]>>) { function.return }
  }
}
// -----

// TEST: 'felt' type global initialized with unparsable attribute
module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{custom op 'global.def' expected integer value}}
  // expected-error@+1 {{custom op 'global.def' failed to parse LLZK_FeltConstAttr parameter 'value' which is to be a `::llvm::APInt`}}
  global.def const @g : !felt.type = unparseable
}
// -----

// TEST: any other type global initialized with unparsable attribute
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{expected attribute value}}
  global.def const @g : index = unparseable
}
// -----

// TEST: 'felt' type global initialized with incorrect attribute kind
module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{custom op 'global.def' expected integer value}}
  // expected-error@+1 {{custom op 'global.def' failed to parse LLZK_FeltConstAttr parameter 'value' which is to be a `::llvm::APInt`}}
  global.def const @g : !felt.type = "Hello"
}
// -----

// TESTS: any other type global initialized with incorrect attribute kind
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{integer constant out of range for attribute}}
  global.def const @g : i1 = 123
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{integer constant out of range for attribute}}
  global.def const @g : i1 = -2
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op with type 'i1' expected builtin.bool or builtin.integer attribute value but found builtin.array}}
  global.def const @g : i1 = [7,8,0]
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op with type 'index' expected builtin.index attribute value but found builtin.bool}}
  global.def const @g : index = false
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op with type 'index' expected builtin.index attribute value but found builtin.string}}
  global.def const @g : index = "hello"
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{integer literal not valid for specified type}}
  global.def const @g : !string.type = 65723456
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op with type '!array.type<2 x !string.type>' expected builtin.array attribute value but found builtin.string}}
  global.def const @g : !array.type<2 x !string.type> = "a string and something else"
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op with type '!array.type<2 x !string.type>' expected builtin.array attribute value to contain 2 elements but found 0}}
  global.def const @g : !array.type<2 x !string.type> = []
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op with type '!array.type<2 x !string.type>' expected builtin.string array element but found builtin.integer}}
  global.def const @g : !array.type<2 x !string.type> = [7, 8]
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op with type '!array.type<2 x !string.type>' expected builtin.array attribute value to contain 2 elements but found 3}}
  global.def const @g : !array.type<2 x !string.type> = ["too", "many", "strings"]
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{'global.def' op with type '!array.type<4 x !string.type>' expected builtin.string array element but found builtin.integer}}
  // expected-error@+1 {{'global.def' op with type '!array.type<4 x !string.type>' expected builtin.string array element but found builtin.affine_map}}
  global.def const @g : !array.type<4 x !string.type> = ["a string and something else", false, affine_map<(i)->(i)>, -100]
}
// -----
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op with type '!array.type<2,2,1,2 x !felt.type>' expected builtin.array attribute value to contain 8 elements but found 4}}
  global.def const @g : !array.type<2,2,1,2 x !felt.type> = [6, 12134, 45523, 15623]
}
// -----

// TEST: 'felt' type global initialized with incorrect attribute kind
module attributes {veridise.lang = "llzk"} {
  // expected-error@+2 {{custom op 'global.def' expected integer value}}
  // expected-error@+1 {{custom op 'global.def' failed to parse LLZK_FeltConstAttr parameter 'value' which is to be a `::llvm::APInt`}}
  global.def const @g : !felt.type = "Hello"
}
// -----

// TEST: global with "const" not initialized
module attributes {veridise.lang = "llzk"} {
  // expected-error@+1 {{'global.def' op marked as 'const' must be assigned a value}}
  global.def const @g : !string.type
}
// -----

// TEST: GlobalReadOp with unknown symbol name
module attributes {veridise.lang = "llzk"} {
  global.def @g : !string.type = "Hello"
  struct.def @Component02 {
    function.def @compute() -> !struct.type<@Component02> {
      // expected-error@+1 {{'global.read' op references unknown symbol "@unknown"}}
      %t = global.read @unknown : !string.type
      %self = struct.new : !struct.type<@Component02>
      function.return %self : !struct.type<@Component02>
    }
    function.def @constrain(%self: !struct.type<@Component02>) { function.return }
  }
}
// -----

// TEST: GlobalReadOp with type that does not match the GlobalDefOp
module attributes {veridise.lang = "llzk"} {
  global.def @g : !felt.type = 6546
  struct.def @Component03 {
    function.def @compute() -> !struct.type<@Component03> {
      // expected-error@+1 {{'global.read' op has wrong type; expected '!felt.type', got '!string.type'}}
      %t = global.read @g : !string.type
      %self = struct.new : !struct.type<@Component03>
      function.return %self : !struct.type<@Component03>
    }
    function.def @constrain(%self: !struct.type<@Component03>) { function.return }
  }
}
// -----

// TEST: GlobalWriteOp with unknown symbol name
module attributes {veridise.lang = "llzk"} {
  global.def @g : !felt.type = 125
  struct.def @Component04 {
    function.def @compute() -> !struct.type<@Component04> {
      %c = felt.const 99999
      // expected-error@+1 {{'global.write' op references unknown symbol "@unknown"}}
      global.write @unknown = %c : !felt.type
      %self = struct.new : !struct.type<@Component04>
      function.return %self : !struct.type<@Component04>
    }
    function.def @constrain(%self: !struct.type<@Component04>) { function.return }
  }
}
// -----

// TEST: GlobalWriteOp with type that does not match the GlobalDefOp
module attributes {veridise.lang = "llzk"} {
  global.def @g : index = 125
  struct.def @Component05 {
    function.def @compute() -> !struct.type<@Component05> {
      %c = felt.const 99999
      // expected-error@+1 {{'global.write' op has wrong type; expected 'index', got '!felt.type'}}
      global.write @g = %c : !felt.type
      %self = struct.new : !struct.type<@Component05>
      function.return %self : !struct.type<@Component05>
    }
    function.def @constrain(%self: !struct.type<@Component05>) { function.return }
  }
}
// -----

// TEST: GlobalWriteOp with type that does not Value
module attributes {veridise.lang = "llzk"} {
  global.def @g : !felt.type = 125
  struct.def @Component06 {
    function.def @compute() -> !struct.type<@Component06> {
      %c = arith.constant true // expected-note {{prior use here}}
      // expected-error@+1 {{use of value '%c' expects different type than prior uses: '!felt.type' vs 'i1'}}
      global.write @g = %c : !felt.type
      %self = struct.new : !struct.type<@Component06>
      function.return %self : !struct.type<@Component06>
    }
    function.def @constrain(%self: !struct.type<@Component06>) { function.return }
  }
}
// -----

// TEST: GlobalWriteOp targeting a "const" GlobalDefOp
module attributes {veridise.lang = "llzk"} {
  global.def const @t : i1 = true
  struct.def @Component07 {
    function.def @compute() -> !struct.type<@Component07> {
      %c = arith.constant false
      // expected-error@+1 {{'global.write' op cannot target 'global.def' marked as 'const'}}
      global.write @t = %c : i1
      %self = struct.new : !struct.type<@Component07>
      function.return %self : !struct.type<@Component07>
    }
    function.def @constrain(%self: !struct.type<@Component07>) { function.return }
  }
}
// -----

// TEST: Struct parameter conflicts with a global name
module attributes {veridise.lang = "llzk"} {
  global.def const @N : index = 3 // expected-note {{symbol already defined here}}
  // expected-error@+1 {{'struct.def' op parameter name "@N" conflicts with an existing symbol}}
  struct.def @Component08<[@N]> {
    function.def @compute() -> !struct.type<@Component08<[@N]>> {
      %self = struct.new : !struct.type<@Component08<[@N]>>
      function.return %self : !struct.type<@Component08<[@N]>>
    }
    function.def @constrain(%self: !struct.type<@Component08<[@N]>>) { function.return }
  }
}
// -----

// TEST: `writeg` cannot be used in a "constrain" function
module attributes {veridise.lang = "llzk"} {
  global.def @t : i1 = true
  struct.def @Component09 {
    function.def @compute() -> !struct.type<@Component09> {
      %self = struct.new : !struct.type<@Component09>
      function.return %self : !struct.type<@Component09>
    }
    function.def @constrain(%self: !struct.type<@Component09>) {
      %c = arith.constant false
      // expected-error@+1 {{'global.write' op only valid within a 'function.def' with 'function.allow_witness' attribute}}
      global.write @t = %c : i1
      function.return
    }
  }
}
// -----

// TEST: `writeg` cannot be used in a free function
module attributes {veridise.lang = "llzk"} {
  global.def @g : !felt.type
  function.def @test() {
    %c = felt.const 99999
    // expected-error@+1 {{'global.write' op only valid within a 'function.def' with 'function.allow_witness' attribute}}
    global.write @g = %c : !felt.type
    function.return
  }
}
