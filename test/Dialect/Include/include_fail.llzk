// RUN: llzk-opt -I %input_dir -split-input-file -llzk-inline-includes -verify-diagnostics %s

module attributes {llzk.lang} {
  // expected-error@+1 {{'include.from' op expected 'builtin.module' from included file to have "llzk.lang" attribute}}
  include.from "Inputs/header_3.llzk" as @alias1

  function.def @test_func() {
    function.call @alias1::@pkg_two::@my_func_2() : () -> ()
    function.return
  }
}
// -----

module attributes {llzk.lang} {
  module @outer {
    // expected-error@+1 {{'include.from' op expected 'builtin.module' from included file to be unnamed}}
    include.from "Inputs/header_4.llzk" as @alias2

    function.def @test_func() {
      function.call @outer::@alias2::@pkg_two::@my_func_2() : () -> ()
      function.return
    }
  }
}
// -----

module attributes {llzk.lang} {
  module @a {
    module @b {
      module @c {
        // expected-error@+1 {{'include.from' op could not find file "Inputs/does_not_exist.llzk"}}
        include.from "Inputs/does_not_exist.llzk" as @alias3
      }
    }
  }

  function.def @test_func() {
    // expected-error@+2 {{'function.call' op references unknown symbol "@a::@b::@c::@alias3::@pkg_two::@my_func_2"}}
    // expected-error@+1 {{expected 'function.def' named "@a::@b::@c::@alias3::@pkg_two::@my_func_2"}}
    function.call @a::@b::@c::@alias3::@pkg_two::@my_func_2() : () -> ()
    function.return
  }
}
// -----

module attributes {llzk.lang} {
  // Note: there's also an error within this included file
  include.from "Inputs/header_5.llzk" as @double
  // expected-error@+1 {{'include.from' op expected 'builtin.module' from included file to be unnamed}}
  include.from "Inputs/header_4.llzk" as @single
}
// -----

module attributes {llzk.lang} {
  // expected-error@+1 {{'include.from' op could not parse file "Inputs/header_6.llzk"}}
  include.from "Inputs/header_6.llzk" as @a
}
// -----

module attributes {llzk.lang} {
  include.from "Inputs/header_7A.llzk" as @a // expected-note {{included from here}}
}
// -----

module attributes {llzk.lang} {
  include.from "Inputs/header_7A.llzk" as @a // expected-note {{included from here}}
  function.def @test_func_A() {
    // Note: The error only occurs during inlining, not during the symbol
    //  lookup because that process is bounded by the SymbolRefAttr itself.
    function.call @a::@b::@c::@func_in_7c() : () -> ()
    function.return
  }
}
// -----

module attributes {llzk.lang} {
  include.from "Inputs/header_7A.llzk" as @entry // expected-note {{included from here}}
  function.def @test_func_B() {
    // Note: Even in this case, symbol lookup will traverse the include cycle but
    //  again, it's bounded by the SymbolRefAttr itself so it will only traverse twice.
    function.call @entry::@b::@c::@cycle::@b::@c::@cycle::@b::@c::@func_in_7c() : () -> ()
    function.return
  }
}
// -----
