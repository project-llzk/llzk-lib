// RUN: llzk-opt -split-input-file -verify-diagnostics %s

module attributes {veridise.lang = "llzk"} {
  struct.def @emiteq_in_compute {
    function.def @compute(%a: !felt.type, %b: !felt.type) -> !struct.type<@emiteq_in_compute> {
      // expected-error@+1 {{'constrain.eq' op only valid within a 'function.def' with 'function.allow_constraint' attribute}}
      constrain.eq %a, %b : !felt.type
      %self = struct.new : !struct.type<@emiteq_in_compute>
      function.return %self : !struct.type<@emiteq_in_compute>
    }

    function.def @constrain(%self: !struct.type<@emiteq_in_compute>, %a: !felt.type, %b: !felt.type) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @emiteq_in_compute_in_loop {
    function.def @compute(%a: !felt.type, %b: !felt.type) -> !struct.type<@emiteq_in_compute_in_loop> {
      %lb = arith.constant 0 : index
      %up = arith.constant 4 : index
      %step = arith.constant 1 : index
      scf.for %iv = %lb to %up step %step {
        // expected-error@+1 {{'constrain.eq' op only valid within a 'function.def' with 'function.allow_constraint' attribute}}
        constrain.eq %a, %b : !felt.type
        constrain.eq %a, %b : !felt.type
      }
      %self = struct.new : !struct.type<@emiteq_in_compute_in_loop>
      function.return %self : !struct.type<@emiteq_in_compute_in_loop>
    }

    function.def @constrain(%self: !struct.type<@emiteq_in_compute_in_loop>, %a: !felt.type, %b: !felt.type) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @emiteq_in_compute_in_deep_branches {
    function.def @compute(%a: i1, %b: i1, %c: i1, %x: !felt.type, %y: !felt.type) -> !struct.type<@emiteq_in_compute_in_deep_branches> {
      scf.if %a {
        scf.if %b {
          scf.if %c {
            // expected-error@+1 {{'constrain.eq' op only valid within a 'function.def' with 'function.allow_constraint' attribute}}
            constrain.eq %x, %y : !felt.type
            constrain.eq %x, %y : !felt.type
          }
        }
      }
      %self = struct.new : !struct.type<@emiteq_in_compute_in_deep_branches>
      function.return %self : !struct.type<@emiteq_in_compute_in_deep_branches>
    }

    function.def @constrain(%self: !struct.type<@emiteq_in_compute_in_deep_branches>, %a: i1, %b: i1, %c: i1, %x: !felt.type, %y: !felt.type) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @emitin_in_compute {
    function.def @compute(%a: !felt.type, %b: !array.type<5 x !felt.type>) -> !struct.type<@emitin_in_compute> {
      // expected-error@+1 {{'constrain.in' op only valid within a 'function.def' with 'function.allow_constraint' attribute}}
      constrain.in %b, %a : !array.type<5 x !felt.type>, !felt.type
      %self = struct.new : !struct.type<@emitin_in_compute>
      function.return %self : !struct.type<@emitin_in_compute>
    }

    function.def @constrain(%self: !struct.type<@emitin_in_compute>, %a: !felt.type, %b: !array.type<5 x !felt.type>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @emitin_in_compute_in_loop {
    function.def @compute(%a: !felt.type, %b: !array.type<5 x !felt.type>) -> !struct.type<@emitin_in_compute_in_loop> {
      %lb = arith.constant 0 : index
      %up = arith.constant 4 : index
      %step = arith.constant 1 : index
      scf.for %iv = %lb to %up step %step {
        // expected-error@+1 {{'constrain.in' op only valid within a 'function.def' with 'function.allow_constraint' attribute}}
        constrain.in %b, %a : !array.type<5 x !felt.type>, !felt.type
        constrain.in %b, %a : !array.type<5 x !felt.type>, !felt.type
      }
      %self = struct.new : !struct.type<@emitin_in_compute_in_loop>
      function.return %self : !struct.type<@emitin_in_compute_in_loop>
    }

    function.def @constrain(%self: !struct.type<@emitin_in_compute_in_loop>, %a: !felt.type, %b: !array.type<5 x !felt.type>) { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @emitin_in_compute_in_deep_branches {
    function.def @compute(%a: i1, %b: i1, %c: i1, %x: index, %y: !array.type<5 x index>) -> !struct.type<@emitin_in_compute_in_deep_branches> {
      scf.if %a {
        scf.if %b {
          scf.if %c {
            // expected-error@+1 {{'constrain.in' op only valid within a 'function.def' with 'function.allow_constraint' attribute}}
            constrain.in %y, %x : !array.type<5 x index>, index
            constrain.in %y, %x : !array.type<5 x index>, index
          }
        }
      }
      %self = struct.new : !struct.type<@emitin_in_compute_in_deep_branches>
      function.return %self : !struct.type<@emitin_in_compute_in_deep_branches>
    }

    function.def @constrain(%self: !struct.type<@emitin_in_compute_in_deep_branches>, %a: i1, %b: i1, %c: i1, %x: index, %y: !array.type<5 x index>) { function.return }
  }
}
// -----
function.def @constrain(%a: !felt.type, %b: !felt.type) {
  // expected-error@+1 {{'constrain.eq' op only valid within a 'function.def' with 'function.allow_constraint' attribute}}
  constrain.eq %a, %b : !felt.type
  constrain.eq %a, %b : !felt.type
  function.return
}
// -----
function.def @constrain(%x: index, %y: !array.type<5 x index>) {
  // expected-error@+1 {{'constrain.in' op only valid within a 'function.def' with 'function.allow_constraint' attribute}}
  constrain.in %y, %x : !array.type<5 x index>, index
  constrain.in %y, %x : !array.type<5 x index>, index
  function.return
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @constrain_calls_compute {
    function.def @compute() -> !struct.type<@constrain_calls_compute> {
      %self = struct.new : !struct.type<@constrain_calls_compute>
      function.return %self : !struct.type<@constrain_calls_compute>
    }

    function.def @constrain(%self: !struct.type<@constrain_calls_compute>) {
      // expected-error@+1 {{'function.call' op target '@compute' has 'function.allow_witness' attribute, which is not specified by the caller '@constrain'}}
      function.call @constrain_calls_compute::@compute() : () -> !struct.type<@constrain_calls_compute>
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @compute_calls_constrain {
    function.def @compute() -> !struct.type<@compute_calls_constrain> {
      %self = struct.new : !struct.type<@compute_calls_constrain>
      // expected-error@+1 {{'function.call' op target '@constrain' has 'function.allow_constraint' attribute, which is not specified by the caller '@compute'}}
      function.call @compute_calls_constrain::@constrain(%self) : (!struct.type<@compute_calls_constrain>) -> ()
      function.return %self : !struct.type<@compute_calls_constrain>
    }

    function.def @constrain(%self: !struct.type<@compute_calls_constrain>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def @globalFunc() {
    // expected-error@+1 {{'function.call' op target '@compute' has 'function.allow_witness' attribute, which is not specified by the caller '@globalFunc'}}
    function.call @global_calls_compute::@compute() : () -> !struct.type<@global_calls_compute>
    function.return
  }
  struct.def @global_calls_compute {
    function.def @compute() -> !struct.type<@global_calls_compute> {
      %self = struct.new : !struct.type<@global_calls_compute>
      function.return %self : !struct.type<@global_calls_compute>
    }

    function.def @constrain(%self: !struct.type<@global_calls_compute>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def @globalFunc(%0: !struct.type<@global_calls_constrain>) {
    // expected-error@+1 {{'function.call' op target '@constrain' has 'function.allow_constraint' attribute, which is not specified by the caller '@globalFunc'}}
    function.call @global_calls_constrain::@constrain(%0) : (!struct.type<@global_calls_constrain>) -> ()
    function.return
  }
  struct.def @global_calls_constrain {
    function.def @compute() -> !struct.type<@global_calls_constrain> {
      %self = struct.new : !struct.type<@global_calls_constrain>
      function.return %self : !struct.type<@global_calls_constrain>
    }

    function.def @constrain(%self: !struct.type<@global_calls_constrain>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @unknown_call_target<[@S]> {
    function.def @compute() -> !struct.type<@unknown_call_target<[@S]>> {
      %self = struct.new : !struct.type<@unknown_call_target<[@S]>>
      function.return %self : !struct.type<@unknown_call_target<[@S]>>
    }

    function.def @constrain(%self: !struct.type<@unknown_call_target<[@S]>>) {
      // expected-error@+2 {{'function.call' op target '@S::@compute' has 'function.allow_witness' attribute, which is not specified by the caller '@constrain'}}
      // expected-error@+1 {{'function.call' op target "@compute" must have exactly one return type}}
      function.call @S::@compute() : () -> ()
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @unknown_call_target<[@S]> {
    function.def @compute() -> !struct.type<@unknown_call_target<[@S]>> {
      %self = struct.new : !struct.type<@unknown_call_target<[@S]>>
      // expected-error@+3 {{'function.call' op target "@constrain" must have no return type}}
      // expected-error@+2 {{'function.call' op target "@constrain" must have at least one input type}}
      // expected-error@+1 {{'function.call' op target '@S::@constrain' has 'function.allow_constraint' attribute, which is not specified by the caller '@compute'}}
      %wrong = function.call @S::@constrain() : () -> (!felt.type)
      function.return %self : !struct.type<@unknown_call_target<[@S]>>
    }

    function.def @constrain(%self: !struct.type<@unknown_call_target<[@S]>>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  function.def @globalFuncPermissive() attributes {function.allow_constraint, function.allow_witness} {
    function.return
  }
  function.def @globalFuncRestrictive() {
    // expected-error@+2 {{'function.call' op target '@globalFuncPermissive' has 'function.allow_constraint' attribute, which is not specified by the caller '@globalFuncRestrictive'}}
    // expected-error@+1 {{'function.call' op target '@globalFuncPermissive' has 'function.allow_witness' attribute, which is not specified by the caller '@globalFuncRestrictive'}}
    function.call @globalFuncPermissive() : () -> ()
    function.return
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @inv_in_constrain {
    function.def @compute(%a: !felt.type) -> !struct.type<@inv_in_constrain> {
      %self = struct.new : !struct.type<@inv_in_constrain>
      function.return %self : !struct.type<@inv_in_constrain>
    }

    function.def @constrain(%self: !struct.type<@inv_in_constrain>, %a: !felt.type) {
      // expected-error@+1 {{'felt.inv' op only valid within a 'function.def' with 'function.allow_non_native_field_ops' attribute}}
      %c = felt.inv %a
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @compute_no_ret {
    // expected-error@+1 {{'function.def' op "@compute" must have exactly one return type}}
    function.def @compute() {
      function.return
    }

    function.def @constrain(%self: !struct.type<@compute_no_ret>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @call_target_compute_no_ret<[@S]> {
    function.def @compute() -> !struct.type<@call_target_compute_no_ret<[@S]>> {
      %self = struct.new : !struct.type<@call_target_compute_no_ret<[@S]>>
      // expected-error@+1 {{'function.call' op target "@compute" must have exactly one return type}}
      function.call @S::@compute() : () -> ()
      function.return %self : !struct.type<@call_target_compute_no_ret<[@S]>>
    }

    function.def @constrain(%self: !struct.type<@call_target_compute_no_ret<[@S]>>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @compute_multiple_ret {
    // expected-error@+1 {{'function.def' op "@compute" must have exactly one return type}}
    function.def @compute(%a: i1) -> (!struct.type<@compute_multiple_ret>, i1) {
      %self = struct.new : !struct.type<@compute_multiple_ret>
      function.return %self, %a : !struct.type<@compute_multiple_ret>, i1
    }

    function.def @constrain(%self: !struct.type<@compute_multiple_ret>, %a: i1) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @call_target_compute_multiple_ret<[@S]> {
    function.def @compute() -> !struct.type<@call_target_compute_multiple_ret<[@S]>> {
      %self = struct.new : !struct.type<@call_target_compute_multiple_ret<[@S]>>
      // expected-error@+1 {{'function.call' op target "@compute" must have exactly one return type}}
      function.call @S::@compute() : () -> (!poly.tvar<@S>, i1)
      function.return %self : !struct.type<@call_target_compute_multiple_ret<[@S]>>
    }

    function.def @constrain(%self: !struct.type<@call_target_compute_multiple_ret<[@S]>>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @constrain_with_ret {
    function.def @compute() -> !struct.type<@constrain_with_ret> {
      %self = struct.new : !struct.type<@constrain_with_ret>
      function.return %self : !struct.type<@constrain_with_ret>
    }

    // expected-error@+1 {{'function.def' op "@constrain" must have no return type}}
    function.def @constrain(%self: !struct.type<@constrain_with_ret>, %a: i1) -> i1 {
      function.return %a : i1
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @call_target_constrain_with_ret<[@S]> {
    function.def @compute(%0: !poly.tvar<@S>) -> !struct.type<@call_target_constrain_with_ret<[@S]>> {
      %self = struct.new : !struct.type<@call_target_constrain_with_ret<[@S]>>
      function.return %self : !struct.type<@call_target_constrain_with_ret<[@S]>>
    }

    function.def @constrain(%self: !struct.type<@call_target_constrain_with_ret<[@S]>>, %0: !poly.tvar<@S>) {
      // expected-error@+1 {{'function.call' op target "@constrain" must have no return type}}
      function.call @S::@constrain(%0) : (!poly.tvar<@S>) -> (!poly.tvar<@S>, i1)
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @compute_with_wrong_ret {
    // expected-error@+1 {{'function.def' op "@compute" must use type of its ancestor 'struct.def' "@compute_with_wrong_ret" as return type}}
    function.def @compute() -> index {
      %b = arith.constant 2 : index
      function.return %b : index
    }

    function.def @constrain(%self: !struct.type<@compute_with_wrong_ret>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @call_target_compute_wrong_type_ret<[@S,@T]> {
    function.def @compute() -> !struct.type<@call_target_compute_wrong_type_ret<[@S,@T]>> {
      %self = struct.new : !struct.type<@call_target_compute_wrong_type_ret<[@S,@T]>>
      // expected-error@+1 {{'function.call' op target "@compute" expected return type '!poly.tvar<@S>' but found 'i1'}}
      function.call @S::@compute() : () -> i1
      function.return %self : !struct.type<@call_target_compute_wrong_type_ret<[@S,@T]>>
    }

    function.def @constrain(%self: !struct.type<@call_target_compute_wrong_type_ret<[@S,@T]>>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @call_target_compute_wrong_tvar_param_ret<[@S,@T]> {
    function.def @compute() -> !struct.type<@call_target_compute_wrong_tvar_param_ret<[@S,@T]>> {
      %self = struct.new : !struct.type<@call_target_compute_wrong_tvar_param_ret<[@S,@T]>>
      // expected-error@+1 {{'function.call' op target "@compute" expected return type '!poly.tvar<@S>' but found '!poly.tvar<@T>'}}
      function.call @S::@compute() : () -> !poly.tvar<@T>
      function.return %self : !struct.type<@call_target_compute_wrong_tvar_param_ret<[@S,@T]>>
    }

    function.def @constrain(%self: !struct.type<@call_target_compute_wrong_tvar_param_ret<[@S,@T]>>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @good { // expected-note {{uses this type instead}}
    function.def @compute() -> !struct.type<@good> {
      %self = struct.new : !struct.type<@good>
      function.return %self : !struct.type<@good>
    }
    function.def @constrain(%self: !struct.type<@good>) {
      function.return
    }
  }

  struct.def @compute_with_wrong_ret {
    // expected-error@+1 {{'function.def' op "@compute" must use type of its ancestor 'struct.def' "@compute_with_wrong_ret" as return type}}
    function.def @compute() -> !struct.type<@good> {
      %self = struct.new : !struct.type<@good>
      function.return %self : !struct.type<@good>
    }

    function.def @constrain(%self: !struct.type<@compute_with_wrong_ret>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  module @one {
    struct.def @good { // expected-note {{uses this type instead}}
      function.def @compute() -> !struct.type<@one::@good> {
        %self = struct.new : !struct.type<@one::@good>
        function.return %self : !struct.type<@one::@good>
      }
      function.def @constrain(%self: !struct.type<@one::@good>) {
        function.return
      }
    }
  }
  module @two {
    struct.def @good {
      // expected-error@+1 {{'function.def' op "@compute" must use type of its ancestor 'struct.def' "@two::@good" as return type}}
      function.def @compute() -> !struct.type<@one::@good> {
        %self = struct.new : !struct.type<@one::@good>
        function.return %self : !struct.type<@one::@good>
      }
      function.def @constrain(%self: !struct.type<@one::@good>) {
        function.return
      }
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  module { // expected-note {{unnamed 'builtin.module' here}}
    module @one {
      struct.def @good {
        // expected-error@+2 {{'function.def' op has ancestor 'builtin.module' without "veridise.lang" attribute or a name}}
        // expected-error@+1 {{could not find 'struct.def' named "@one::@good"}}
        function.def @compute() -> !struct.type<@one::@good> {
          %self = struct.new : !struct.type<@one::@good>
          function.return %self : !struct.type<@one::@good>
        }
        function.def @constrain(%self: !struct.type<@one::@good>) {
          function.return
        }
      }
    }
  }
}
// -----
module { // expected-note {{unnamed 'builtin.module' here}}
  // expected-error@+1 {{'struct.def' op has ancestor 'builtin.module' without "veridise.lang" attribute or a name}}
  struct.def @compute_with_wrong_ret {
    // expected-error@+1 {{'function.def' op "@compute" must use type of its ancestor 'struct.def' "@compute_with_wrong_ret" as return type}}
    function.def @compute() -> index {
      %b = arith.constant 2 : index
      function.return %b : index
    }

    function.def @constrain(%self: !struct.type<@compute_with_wrong_ret>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @constrain_without_args {
    function.def @compute() -> !struct.type<@constrain_without_args> {
      %self = struct.new : !struct.type<@constrain_without_args>
      function.return %self : !struct.type<@constrain_without_args>
    }
    // expected-error@+1 {{'function.def' op "@constrain" must have at least one input type}}
    function.def @constrain() { function.return }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @call_target_constrain_without_args<[@S]> {
    function.def @compute() -> !struct.type<@call_target_constrain_without_args<[@S]>> {
      %self = struct.new : !struct.type<@call_target_constrain_without_args<[@S]>>
      function.return %self : !struct.type<@call_target_constrain_without_args<[@S]>>
    }

    function.def @constrain(%self: !struct.type<@call_target_constrain_without_args<[@S]>>) {
      // expected-error@+1 {{'function.call' op target "@constrain" must have at least one input type}}
      function.call @S::@constrain() : () -> ()
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @constrain_without_self_non_struct {
    function.def @compute(%a: !felt.type) -> !struct.type<@constrain_without_self_non_struct> {
      %self = struct.new : !struct.type<@constrain_without_self_non_struct>
      function.return %self : !struct.type<@constrain_without_self_non_struct>
    }
    // expected-error@+1 {{'function.def' op "@constrain" must use type of its ancestor 'struct.def' "@constrain_without_self_non_struct" as first input type}}
    function.def @constrain(%self: !felt.type) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @call_target_constrain_without_self_non_struct<[@S]> {
    function.def @compute() -> !struct.type<@call_target_constrain_without_self_non_struct<[@S]>> {
      %self = struct.new : !struct.type<@call_target_constrain_without_self_non_struct<[@S]>>
      function.return %self : !struct.type<@call_target_constrain_without_self_non_struct<[@S]>>
    }

    function.def @constrain(%self: !struct.type<@call_target_constrain_without_self_non_struct<[@S]>>) {
      %0 = felt.const 1
      // expected-error@+1 {{'function.call' op target "@constrain" expected first input type '!poly.tvar<@S>' but found '!felt.type'}}
      function.call @S::@constrain(%0) : (!felt.type) -> ()
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @other_type { // expected-note {{uses this type instead}}
    function.def @compute() -> !struct.type<@other_type> {
      %self = struct.new : !struct.type<@other_type>
      function.return %self : !struct.type<@other_type>
    }
    function.def @constrain(%self: !struct.type<@other_type>) {
      function.return
    }
  }
  struct.def @constrain_without_self_wrong_struct {
    function.def @compute() -> !struct.type<@constrain_without_self_wrong_struct> {
      %self = struct.new : !struct.type<@constrain_without_self_wrong_struct>
      function.return %self : !struct.type<@constrain_without_self_wrong_struct>
    }
    // expected-error@+1 {{'function.def' op "@constrain" must use type of its ancestor 'struct.def' "@constrain_without_self_wrong_struct" as first input type}}
    function.def @constrain(%self: !struct.type<@other_type>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @call_target_constrain_without_self_wrong_tvar_param<[@S,@T]> {
    function.def @compute(%0: !poly.tvar<@T>) -> !struct.type<@call_target_constrain_without_self_wrong_tvar_param<[@S,@T]>> {
      %self = struct.new : !struct.type<@call_target_constrain_without_self_wrong_tvar_param<[@S,@T]>>
      function.return %self : !struct.type<@call_target_constrain_without_self_wrong_tvar_param<[@S,@T]>>
    }

    function.def @constrain(%self: !struct.type<@call_target_constrain_without_self_wrong_tvar_param<[@S,@T]>>, %0: !poly.tvar<@T>) {
      // expected-error@+1 {{'function.call' op target "@constrain" expected first input type '!poly.tvar<@S>' but found '!poly.tvar<@T>}}
      function.call @S::@constrain(%0) : (!poly.tvar<@T>) -> ()
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @mismatch_arg_types_1 {
    // expected-note@+1 {{"@compute" function defined here}}
    function.def @compute(%a: !felt.type) -> !struct.type<@mismatch_arg_types_1> {
      %self = struct.new : !struct.type<@mismatch_arg_types_1>
      function.return %self : !struct.type<@mismatch_arg_types_1>
    }
    // expected-error@+1 {{expected "@constrain" function argument types (sans the first one) to match "@compute" function argument types}}
    function.def @constrain(%self: !struct.type<@mismatch_arg_types_1>) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @mismatch_arg_types_2 {
    // expected-note@+1 {{"@compute" function defined here}}
    function.def @compute() -> !struct.type<@mismatch_arg_types_2> {
      %self = struct.new : !struct.type<@mismatch_arg_types_2>
      function.return %self : !struct.type<@mismatch_arg_types_2>
    }
    // expected-error@+1 {{expected "@constrain" function argument types (sans the first one) to match "@compute" function argument types}}
    function.def @constrain(%self: !struct.type<@mismatch_arg_types_2>, %a: !felt.type) {
      function.return
    }
  }
}
// -----
module attributes {veridise.lang = "llzk"} {
  struct.def @mismatch_arg_types_3 {
    // expected-note@+1 {{"@compute" function defined here}}
    function.def @compute(%a: !felt.type) -> !struct.type<@mismatch_arg_types_3> {
      %self = struct.new : !struct.type<@mismatch_arg_types_3>
      function.return %self : !struct.type<@mismatch_arg_types_3>
    }
    // expected-error@+1 {{expected "@constrain" function argument types (sans the first one) to match "@compute" function argument types}}
    function.def @constrain(%self: !struct.type<@mismatch_arg_types_3>, %a: i1) {
      function.return
    }
  }
}

// -----

#map1 = affine_map<()[s0, s1] -> (s0 + s1)>
module attributes {veridise.lang = "llzk"} {
  struct.def @T2<[@N]> {
    function.def @compute() -> !struct.type<@T2<[@N]>> {
      %self = struct.new : <@T2<[@N]>>
      function.return %self : !struct.type<@T2<[@N]>>
    }
    function.def @constrain(%self : !struct.type<@T2<[@N]>>) {
      function.return
    }
  }
  struct.def @Tester<[@S0, @S1]> {
    // expected-error@+1 {{'function.def' op "@compute" parameters cannot contain affine map attributes but found '!struct.type<@T2<[affine_map<()[s0, s1] -> (s0 + s1)>]>>}}
    function.def @compute(%6: !struct.type<@T2<[#map1]>>) -> !struct.type<@Tester<[@S0, @S1]>> {
      %self = struct.new : <@Tester<[@S0, @S1]>>
      function.return %self : !struct.type<@Tester<[@S0, @S1]>>
    }
    function.def @constrain(%self: !struct.type<@Tester<[@S0, @S1]>>, %6: !struct.type<@T2<[#map1]>>) {
      function.return
    }
  }
}

// -----

#map1 = affine_map<()[s0, s1] -> (s0 + s1)>
module attributes {veridise.lang = "llzk"} {
  struct.def @T2<[@N]> {
    function.def @compute() -> !struct.type<@T2<[@N]>> {
      %self = struct.new : <@T2<[@N]>>
      function.return %self : !struct.type<@T2<[@N]>>
    }
    function.def @constrain(%self : !struct.type<@T2<[@N]>>) {
      function.return
    }
  }
  struct.def @Tester<[@S0, @S1]> {
     // expected-error@+1 {{'function.def' op "@constrain" parameters cannot contain affine map attributes but found '!struct.type<@T2<[affine_map<()[s0, s1] -> (s0 + s1)>]>>}}
    function.def @constrain(%self: !struct.type<@Tester<[@S0, @S1]>>, %6: !struct.type<@T2<[#map1]>>) {
      function.return
    }
    function.def @compute(%6: !struct.type<@T2<[#map1]>>) -> !struct.type<@Tester<[@S0, @S1]>> {
      %self = struct.new : <@Tester<[@S0, @S1]>>
      function.return %self : !struct.type<@Tester<[@S0, @S1]>>
    }
  }
}

// -----
builtin.module attributes {veridise.lang = "llzk"} {
  function.def @free_func() {
    // expected-error@+1 {{'builtin.module' op cannot be nested within 'function.def' operations}}
    builtin.module {}
    function.return
  }
}
// -----
builtin.module attributes {veridise.lang = "llzk"} {
  function.def @free_func() {
    // expected-error@+1 {{'struct.def' op expects parent op 'builtin.module'}}
    struct.def @ComponentG {
      function.def @compute() -> !struct.type<@ComponentG> {
        %self = struct.new : !struct.type<@ComponentG>
        function.return %self : !struct.type<@ComponentG>
      }
      function.def @constrain(%self: !struct.type<@ComponentG>)  {
        function.return
      }
    }
    function.return
  }
}
// -----
builtin.module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentG {
    function.def @compute() -> !struct.type<@ComponentG> {
      %self = struct.new : !struct.type<@ComponentG>
      // expected-error@+1 {{'builtin.module' op cannot be nested within 'function.def' operations}}
      builtin.module attributes {veridise.lang = "llzk"} {}
      function.return %self : !struct.type<@ComponentG>
    }
    function.def @constrain(%self: !struct.type<@ComponentG>)  {
      function.return
    }
  }
}
// -----
builtin.module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentG {
    function.def @compute() -> !struct.type<@ComponentG> {
      %self = struct.new : !struct.type<@ComponentG>
      // expected-error@+1 {{'builtin.module' op cannot be nested within 'function.def' operations}}
      builtin.module attributes {veridise.lang = "llzk"} {
        struct.def @ComponentX {
          function.def @compute() -> !struct.type<@ComponentX> {
            %self2 = struct.new : !struct.type<@ComponentX>
            function.return %self2 : !struct.type<@ComponentX>
          }
          function.def @constrain(%self2: !struct.type<@ComponentX>)  {
            function.return
          }
        }
      }
      function.return %self : !struct.type<@ComponentG>
    }
    function.def @constrain(%self: !struct.type<@ComponentG>)  {
      function.return
    }
  }
}
