// RUN: llzk-opt -I %input_dir -split-input-file -verify-diagnostics %s

// -----
module attributes {llzk.lang} {
  include.from "Inputs/header_11.llzk" as @Lib

  struct.def @MissingInstantiations {
    function.def @compute(%n: index) -> !struct.type<@MissingInstantiations> {
      %self = struct.new : !struct.type<@MissingInstantiations>
      // expected-error@+1 {{'function.call' op map instantiation group count (0) does not match the number of affine map instantiations (1) required by the type}}
      %x = function.call @Lib::@D1::@compute() : () -> !struct.type<@Lib::@D1<[affine_map<(i)->(i)>]>>
      function.return %self : !struct.type<@MissingInstantiations>
    }

    function.def @constrain(%self: !struct.type<@MissingInstantiations>, %n: index) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  include.from "Inputs/header_11.llzk" as @Lib

  struct.def @EmptyInstantiations {
    function.def @compute(%n: index) -> !struct.type<@EmptyInstantiations> {
      %self = struct.new : !struct.type<@EmptyInstantiations>
      // expected-error@+1 {{expected '(' in operand list}}
      %x = function.call @Lib::@D1::@compute(){} : () -> !struct.type<@Lib::@D1<[affine_map<(i)->(i)>]>>
      function.return %self : !struct.type<@EmptyInstantiations>
    }

    function.def @constrain(%self: !struct.type<@EmptyInstantiations>, %n: index) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  include.from "Inputs/header_11.llzk" as @Lib

  struct.def @TooFewInstantiations {
    function.def @compute(%n: index) -> !struct.type<@TooFewInstantiations> {
      %self = struct.new : !struct.type<@TooFewInstantiations>
      // expected-error@+1 {{'function.call' op map instantiation group count (1) does not match the number of affine map instantiations (2) required by the type}}
      %x = function.call @Lib::@D2::@compute(){(%n)[]} : () -> !struct.type<@Lib::@D2<[affine_map<(i)->(i)>, affine_map<(i)->(i)>]>>
      function.return %self : !struct.type<@TooFewInstantiations>
    }

    function.def @constrain(%self: !struct.type<@TooFewInstantiations>, %n: index) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  include.from "Inputs/header_11.llzk" as @Lib

  struct.def @TooManyInstantiations {
    function.def @compute(%n: index) -> !struct.type<@TooManyInstantiations> {
      %self = struct.new : !struct.type<@TooManyInstantiations>
      // expected-error@+1 {{'function.call' op map instantiation group count (2) does not match the number of affine map instantiations (1) required by the type}}
      %x = function.call @Lib::@D1::@compute(){(%n)[], ()[%n]} : () -> !struct.type<@Lib::@D1<[affine_map<(i)->(i)>]>>
      function.return %self : !struct.type<@TooManyInstantiations>
    }

    function.def @constrain(%self: !struct.type<@TooManyInstantiations>, %n: index) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  include.from "Inputs/header_11.llzk" as @Lib

  struct.def @TooFewDimensions {
    function.def @compute(%n: index) -> !struct.type<@TooFewDimensions> {
      %self = struct.new : !struct.type<@TooFewDimensions>
      // expected-error@+1 {{'function.call' op instantiation of map 0 expected 1 but found 0 dimension values in ()}}
      %x = function.call @Lib::@D1::@compute(){()[%n]} : () -> !struct.type<@Lib::@D1<[affine_map<(i)->(i)>]>>
      function.return %self : !struct.type<@TooFewDimensions>
    }

    function.def @constrain(%self: !struct.type<@TooFewDimensions>, %n: index) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  include.from "Inputs/header_11.llzk" as @Lib

  struct.def @TooManyDimensions {
    function.def @compute(%a: index, %b: index) -> !struct.type<@TooManyDimensions> {
      %self = struct.new : !struct.type<@TooManyDimensions>
      // expected-error@+1 {{'function.call' op instantiation of map 0 expected 1 but found 2 dimension values in ()}}
      %x = function.call @Lib::@D1::@compute(){ (%a,%b)[] } : () -> !struct.type<@Lib::@D1<[affine_map<(i)->(i)>]>>
      function.return %self : !struct.type<@TooManyDimensions>
    }

    function.def @constrain(%self: !struct.type<@TooManyDimensions>, %a: index, %b: index) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  include.from "Inputs/header_11.llzk" as @Lib

  struct.def @TooFewSymbols {
    function.def @compute(%n: index) -> !struct.type<@TooFewSymbols> {
      %self = struct.new : !struct.type<@TooFewSymbols>
      // expected-error@+1 {{'function.call' op instantiation of map 0 expected 1 but found 0 symbol values in []}}
      %x = function.call @Lib::@D1::@compute(){(%n)[]} : () -> !struct.type<@Lib::@D1<[affine_map<(i)[k] -> (5*i+k)>]>>
      function.return %self : !struct.type<@TooFewSymbols>
    }

    function.def @constrain(%self: !struct.type<@TooFewSymbols>, %n: index) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  include.from "Inputs/header_11.llzk" as @Lib

  struct.def @TooManySymbols {
    function.def @compute(%a: index, %b: index, %c: index) -> !struct.type<@TooManySymbols> {
      %self = struct.new : !struct.type<@TooManySymbols>
      // expected-error@+1 {{'function.call' op instantiation of map 0 expected 1 but found 2 symbol values in []}}
      %x = function.call @Lib::@D1::@compute(){ (%a)[%b, %c] } : () -> !struct.type<@Lib::@D1<[affine_map<(i)[k] -> (5*i+k)>]>>
      function.return %self : !struct.type<@TooManySymbols>
    }

    function.def @constrain(%self: !struct.type<@TooManySymbols>, %a: index, %b: index, %c: index) { function.return }
  }
}
// -----
module attributes {llzk.lang} {
  struct.def @CallUnknownConstrainWithMapInit<[@S]> {
    function.def @compute(%0: !poly.tvar<@S>) -> !struct.type<@CallUnknownConstrainWithMapInit<[@S]>> {
      %self = struct.new : !struct.type<@CallUnknownConstrainWithMapInit<[@S]>>
      function.return %self : !struct.type<@CallUnknownConstrainWithMapInit<[@S]>>
    }

    function.def @constrain(%self: !struct.type<@CallUnknownConstrainWithMapInit<[@S]>>, %0: !poly.tvar<@S>) {
      %i = arith.constant 28 : index
      // expected-error@+1 {{'function.call' op can only have affine map instantiations when targeting a "@compute" function}}
      function.call @S::@constrain(%0){ (%i) } : (!poly.tvar<@S>) -> ()
      function.return
    }
  }
}
// -----
module attributes {llzk.lang} {
  include.from "Inputs/header_11.llzk" as @Lib

  struct.def @CallKnownConstrainWithMapInit {
    function.def @compute(%0: !struct.type<@Lib::@D0>) -> !struct.type<@CallKnownConstrainWithMapInit> {
      %self = struct.new : !struct.type<@CallKnownConstrainWithMapInit>
      function.return %self : !struct.type<@CallKnownConstrainWithMapInit>
    }

    function.def @constrain(%self: !struct.type<@CallKnownConstrainWithMapInit>, %0: !struct.type<@Lib::@D0>) {
      %i = arith.constant 28 : index
      // expected-error@+1 {{'function.call' op can only have affine map instantiations when targeting a "@compute" function}}
      function.call @Lib::@D0::@constrain(%0){ (%i) } : (!struct.type<@Lib::@D0>) -> ()
      function.return
    }
  }
}
// -----
module attributes {llzk.lang} {
  function.def @global_id(%0: index) -> index {
    function.return %0 : index
  }

  struct.def @CallKnownGlobalWithMapInit {
    function.def @compute() -> !struct.type<@CallKnownGlobalWithMapInit> {
      %self = struct.new : !struct.type<@CallKnownGlobalWithMapInit>
      %i = arith.constant 28 : index
      // expected-error@+1 {{'function.call' op can only have affine map instantiations when targeting a "@compute" function}}
      %0 = function.call @global_id(%i){ (%i) } : (index) -> (index)
      function.return %self : !struct.type<@CallKnownGlobalWithMapInit>
    }

    function.def @constrain(%self: !struct.type<@CallKnownGlobalWithMapInit>) { function.return }
  }
}

// -----
module attributes {llzk.lang} {
  include.from "Inputs/header_11.llzk" as @Lib

  struct.def @TooFewResults {
    function.def @compute(%a: index, %b: index, %c: index) -> !struct.type<@TooManySymbols> {
      %self = struct.new : !struct.type<@TooManySymbols>
      // expected-error@+1 {{AffineMapAttr must yield a single result, but found 0 results}}
      %x = function.call @Lib::@D1::@compute(){ (%a)[%b, %c] } : () -> !struct.type<@Lib::@D1<[affine_map<(i)[k, j] -> ()>]>>
      function.return %self : !struct.type<@TooManySymbols>
    }

    function.def @constrain(%self: !struct.type<@TooManySymbols>, %a: index, %b: index, %c: index) { function.return }
  }
}

// -----
module attributes {llzk.lang} {
  include.from "Inputs/header_11.llzk" as @Lib

  struct.def @TooManyResults {
    function.def @compute(%a: index, %b: index, %c: index) -> !struct.type<@TooManySymbols> {
      %self = struct.new : !struct.type<@TooManySymbols>
      // expected-error@+1 {{AffineMapAttr must yield a single result, but found 5 results}}
      %x = function.call @Lib::@D1::@compute(){ (%a)[%b, %c] } : () -> !struct.type<@Lib::@D1<[affine_map<(i)[k, j] -> (5*i+k, j, i, k, 7)>]>>
      function.return %self : !struct.type<@TooManySymbols>
    }

    function.def @constrain(%self: !struct.type<@TooManySymbols>, %a: index, %b: index, %c: index) { function.return }
  }
}
