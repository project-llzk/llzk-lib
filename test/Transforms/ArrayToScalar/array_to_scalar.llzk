// RUN: llzk-opt -split-input-file -llzk-array-to-scalar %s | FileCheck --enable-var-scope %s

module attributes {veridise.lang = "llzk"} {
  function.def @new_then_read() -> index {
    %0 = arith.constant 0 : index
    %1 = arith.constant 1 : index

    %a = arith.constant 45 : index
    %b = arith.constant 214 : index
    %c = arith.constant 15875 : index
    %d = arith.constant 769 : index

    %r = array.new %a, %b, %c, %d, %c, %a: !array.type<2,3 x index>
    %x = array.read %r[%1,%0] : !array.type<2,3 x index>, index
    function.return %x : index
  }
}
// CHECK-LABEL: function.def @new_then_read() -> index {
// CHECK-NEXT:    %[[V:[0-9a-zA-Z_\.]+]] = arith.constant 769 : index
// CHECK-NEXT:    function.return %[[V]] : index
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @multiple_read_and_write() -> !felt.type {
    %0 = arith.constant 0 : index
    %1 = arith.constant 1 : index
    %2 = arith.constant 2 : index

    %a = felt.const 2
    %b = felt.const 3
    %c = felt.const 5
    %d = felt.const 7
    %e = felt.const 11
    %f = felt.const 13
    %g = felt.const 17
    %h = felt.const 19
    %i = felt.const 23
    %j = felt.const 29

    %r = array.new %a, %b, %c, %d, %e, %f, %g, %h: !array.type<4,2 x !felt.type>
    %x = array.read %r[%2,%0] : !array.type<4,2 x !felt.type>, !felt.type
    // overwrite the same index to ensure the read survives
    array.write %r[%1,%0] = %i : !array.type<4,2 x !felt.type>, !felt.type
    // write to a different index and then read it to ensure the new value is used
    array.write %r[%0,%1] = %j : !array.type<4,2 x !felt.type>, !felt.type
    %y = array.read %r[%0,%1] : !array.type<4,2 x !felt.type>, !felt.type
    // e * j
    %z = felt.mul %x, %y
    function.return %z : !felt.type
  }
}
// CHECK-LABEL: function.def @multiple_read_and_write() -> !felt.type {
// CHECK-NEXT:    %[[V0:[0-9a-zA-Z_\.]+]] = felt.const  11
// CHECK-NEXT:    %[[V1:[0-9a-zA-Z_\.]+]] = felt.const  29
// CHECK-NEXT:    %[[V2:[0-9a-zA-Z_\.]+]] = felt.mul %[[V0]], %[[V1]] : !felt.type, !felt.type
// CHECK-NEXT:    function.return %[[V2]] : !felt.type
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @new_then_return() -> !array.type<2,1,2 x !felt.type> {
    %a = felt.const 45
    %b = felt.const 214
    %c = felt.const 2
    %d = felt.const 52

    %r = array.new %a, %b, %c, %d : !array.type<2,1,2 x !felt.type>
    function.return %r: !array.type<2,1,2 x !felt.type>
  }
}
// CHECK-LABEL: function.def @new_then_return() -> (!felt.type, !felt.type, !felt.type, !felt.type) {
// CHECK-NEXT:    %[[V0:[0-9a-zA-Z_\.]+]] = felt.const  45
// CHECK-NEXT:    %[[V1:[0-9a-zA-Z_\.]+]] = felt.const  214
// CHECK-NEXT:    %[[V2:[0-9a-zA-Z_\.]+]] = felt.const  2
// CHECK-NEXT:    %[[V3:[0-9a-zA-Z_\.]+]] = felt.const  52
// CHECK-NEXT:    function.return %[[V0]], %[[V1]], %[[V2]], %[[V3]] : !felt.type, !felt.type, !felt.type, !felt.type
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @len() -> index {
    %a = array.new  : !array.type<6,1,2,1,5,1 x !felt.type>
    %4 = arith.constant 4 : index
    %z = array.len %a, %4 : !array.type<6,1,2,1,5,1 x !felt.type> // result is 5
    function.return %z: index
  }
}
// CHECK-LABEL: function.def @len() -> index {
// CHECK-NEXT:    %[[V0:[0-9a-zA-Z_\.]+]] = arith.constant 5 : index
// CHECK-NEXT:    function.return %[[V0]] : index
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @convert_arg(%a: !array.type<3,1,2,1,4,1 x !felt.type>) -> !felt.type {
    %0 = arith.constant 0 : index
    %1 = arith.constant 1 : index
    %z = array.read %a[%1,%0,%1,%0,%1,%0] : !array.type<3,1,2,1,4,1 x !felt.type>, !felt.type
    function.return %z: !felt.type
  }
}
// CHECK-LABEL: function.def @convert_arg(
// CHECK-SAME:              %[[V0:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V1:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V2:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V3:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V4:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V5:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V6:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V7:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V8:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V9:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V10:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V11:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V12:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V13:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V14:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V15:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V16:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V17:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V18:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V19:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V20:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V21:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V22:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:              %[[V23:[0-9a-zA-Z_\.]+]]: !felt.type) -> !felt.type {
// CHECK-NEXT:    function.return %[[V13]] : !felt.type
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @uninitialized() -> index {
    %0 = arith.constant 0 : index
    %1 = arith.constant 1 : index

    %r = array.new : !array.type<2,3 x index>
    %x = array.read %r[%1,%0] : !array.type<2,3 x index>, index
    function.return %x : index
  }
}
// CHECK-LABEL: function.def @uninitialized() -> index {
// CHECK-NEXT:    %[[V0:[0-9a-zA-Z_\.]+]] = llzk.nondet : index
// CHECK-NEXT:    function.return %[[V0]] : index
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @f1(%arg: !felt.type) -> !array.type<3,2 x !felt.type> {
    %0 = arith.constant 0 : index
    %a = function.call @f2() : () -> !array.type<3,2 x !felt.type>
    array.write %a[%0,%0] = %arg : !array.type<3,2 x !felt.type>, !felt.type
    function.return %a : !array.type<3,2 x !felt.type>
  }

  function.def @f2() -> !array.type<3,2 x !felt.type> {
    %a = array.new : !array.type<3,2 x !felt.type>
    function.return %a : !array.type<3,2 x !felt.type>
  }
}
// CHECK-LABEL: function.def @f1(
// CHECK-SAME:           %[[V0:[0-9a-zA-Z_\.]+]]: !felt.type) -> (!felt.type, !felt.type, !felt.type, !felt.type, !felt.type, !felt.type) {
// CHECK-NEXT:    %[[V1:[0-9a-zA-Z_\.]+]]:6 = function.call @f2() : () -> (!felt.type, !felt.type, !felt.type, !felt.type, !felt.type, !felt.type)
// CHECK-NEXT:    function.return %[[V0]], %[[V1]]#1, %[[V1]]#2, %[[V1]]#3, %[[V1]]#4, %[[V1]]#5 : !felt.type, !felt.type, !felt.type, !felt.type, !felt.type, !felt.type
// CHECK-NEXT:  }
//
// CHECK-LABEL: function.def @f2() -> (!felt.type, !felt.type, !felt.type, !felt.type, !felt.type, !felt.type) {
// CHECK-NEXT:    %[[V0:[0-9a-zA-Z_\.]+]] = llzk.nondet : !felt.type
// CHECK-NEXT:    %[[V1:[0-9a-zA-Z_\.]+]] = llzk.nondet : !felt.type
// CHECK-NEXT:    %[[V2:[0-9a-zA-Z_\.]+]] = llzk.nondet : !felt.type
// CHECK-NEXT:    %[[V3:[0-9a-zA-Z_\.]+]] = llzk.nondet : !felt.type
// CHECK-NEXT:    %[[V4:[0-9a-zA-Z_\.]+]] = llzk.nondet : !felt.type
// CHECK-NEXT:    %[[V5:[0-9a-zA-Z_\.]+]] = llzk.nondet : !felt.type
// CHECK-NEXT:    function.return %[[V0]], %[[V1]], %[[V2]], %[[V3]], %[[V4]], %[[V5]] : !felt.type, !felt.type, !felt.type, !felt.type, !felt.type, !felt.type
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @new_then_write_then_read() -> !felt.type {
    %0 = arith.constant 0 : index
    %1 = arith.constant 1 : index

    %a = felt.const 0
    %b = felt.const 424867
    %c = felt.const 2465
    %d = felt.const 367

    %r = array.new %a, %a, %a, %a: !array.type<2,2 x !felt.type>
    array.write %r[%0,%1] = %b: !array.type<2,2 x !felt.type>, !felt.type
    array.write %r[%0,%0] = %c: !array.type<2,2 x !felt.type>, !felt.type
    array.write %r[%1,%0] = %d: !array.type<2,2 x !felt.type>, !felt.type
    // [[2465, 424867],[367, 0]]
    %w = array.read %r[%0,%0] : !array.type<2,2 x !felt.type>, !felt.type
    %x = array.read %r[%0,%1] : !array.type<2,2 x !felt.type>, !felt.type
    %y = array.read %r[%1,%0] : !array.type<2,2 x !felt.type>, !felt.type
    %z = array.read %r[%1,%1] : !array.type<2,2 x !felt.type>, !felt.type

    %s1 = felt.add %w, %x   // 2465 + 424867
    %s2 = felt.add %s1, %y  // ^ + 367
    %s3 = felt.add %s2, %z  // ^ + 0
    function.return %s3 : !felt.type
  }
}
// CHECK-LABEL: function.def @new_then_write_then_read() -> !felt.type {
// CHECK-NEXT:    %[[V0:[0-9a-zA-Z_\.]+]] = felt.const  0
// CHECK-NEXT:    %[[V1:[0-9a-zA-Z_\.]+]] = felt.const  424867
// CHECK-NEXT:    %[[V2:[0-9a-zA-Z_\.]+]] = felt.const  2465
// CHECK-NEXT:    %[[V3:[0-9a-zA-Z_\.]+]] = felt.const  367
// CHECK-NEXT:    %[[V4:[0-9a-zA-Z_\.]+]] = felt.add %[[V2]], %[[V1]] : !felt.type, !felt.type
// CHECK-NEXT:    %[[V5:[0-9a-zA-Z_\.]+]] = felt.add %[[V4]], %[[V3]] : !felt.type, !felt.type
// CHECK-NEXT:    %[[V6:[0-9a-zA-Z_\.]+]] = felt.add %[[V5]], %[[V0]] : !felt.type, !felt.type
// CHECK-NEXT:    function.return %[[V6]] : !felt.type
// CHECK-NEXT:  }
// -----

!F = !felt.type
module attributes {veridise.lang = "llzk"} {
  struct.def @ArrayMembers {
    struct.member @arr : !array.type<2,3 x !F>

    function.def @compute(%a0: !F, %a1: !F, %a2: !F, %a3: !F, %a4: !F, %a5: !F) -> !struct.type<@ArrayMembers> {
      %self = struct.new : !struct.type<@ArrayMembers>
      %a = array.new %a0, %a1, %a2, %a3, %a4, %a5 : !array.type<2,3 x !F>
      struct.writem %self[@arr] = %a : !struct.type<@ArrayMembers>, !array.type<2,3 x !F>
      function.return %self : !struct.type<@ArrayMembers>
    }

    function.def @constrain(%self: !struct.type<@ArrayMembers>, %a0: !F, %a1: !F, %a2: !F, %a3: !F, %a4: !F, %a5: !F) {
      %a = struct.readm %self[@arr] : !struct.type<@ArrayMembers>, !array.type<2,3 x !F>

      %0 = arith.constant 0 : index
      %1 = arith.constant 1 : index
      %2 = arith.constant 2 : index

      %b0 = array.read %a[%0, %0] : !array.type<2,3 x !F>, !F
      constrain.eq %a0, %b0 : !F

      %b1 = array.read %a[%0, %1] : !array.type<2,3 x !F>, !F
      constrain.eq %a1, %b1 : !F

      %b2 = array.read %a[%0, %2] : !array.type<2,3 x !F>, !F
      constrain.eq %a2, %b2 : !F

      %b3 = array.read %a[%1, %0] : !array.type<2,3 x !F>, !F
      constrain.eq %a3, %b3 : !F

      %b4 = array.read %a[%1, %1] : !array.type<2,3 x !F>, !F
      constrain.eq %a4, %b4 : !F

      %b5 = array.read %a[%1, %2] : !array.type<2,3 x !F>, !F
      constrain.eq %a5, %b5 : !F

      function.return
    }
  }
}
// CHECK-LABEL: struct.def @ArrayMembers {
// CHECK-NEXT:    struct.member @arr_0 : !felt.type
// CHECK-NEXT:    struct.member @arr_1 : !felt.type
// CHECK-NEXT:    struct.member @arr_2 : !felt.type
// CHECK-NEXT:    struct.member @arr_3 : !felt.type
// CHECK-NEXT:    struct.member @arr_4 : !felt.type
// CHECK-NEXT:    struct.member @arr_5 : !felt.type
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V1:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V2:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:    %[[V3:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V4:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V5:[0-9a-zA-Z_\.]+]]: !felt.type) -> !struct.type<@ArrayMembers>  attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V6:[0-9a-zA-Z_\.]+]] = struct.new : <@ArrayMembers>
// CHECK-DAG:       struct.writem %[[V6]][@arr_5] = %[[V5]] : <@ArrayMembers>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_3] = %[[V3]] : <@ArrayMembers>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_4] = %[[V4]] : <@ArrayMembers>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_2] = %[[V2]] : <@ArrayMembers>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_0] = %[[V0]] : <@ArrayMembers>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_1] = %[[V1]] : <@ArrayMembers>, !felt.type
// CHECK-NEXT:      function.return %[[V6]] : !struct.type<@ArrayMembers>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V7:[0-9a-zA-Z_\.]+]]: !struct.type<@ArrayMembers>, %[[V8:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V9:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:    %[[V10:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V11:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V12:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V13:[0-9a-zA-Z_\.]+]]: !felt.type) attributes {function.allow_constraint} {
// CHECK-DAG:       %[[V14:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_5] : <@ArrayMembers>, !felt.type
// CHECK-DAG:       %[[V15:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_3] : <@ArrayMembers>, !felt.type
// CHECK-DAG:       %[[V16:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_4] : <@ArrayMembers>, !felt.type
// CHECK-DAG:       %[[V17:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_2] : <@ArrayMembers>, !felt.type
// CHECK-DAG:       %[[V18:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_0] : <@ArrayMembers>, !felt.type
// CHECK-DAG:       %[[V19:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_1] : <@ArrayMembers>, !felt.type
// CHECK-NEXT:      constrain.eq %[[V8]], %[[V18]] : !felt.type, !felt.type
// CHECK-NEXT:      constrain.eq %[[V9]], %[[V19]] : !felt.type, !felt.type
// CHECK-NEXT:      constrain.eq %[[V10]], %[[V17]] : !felt.type, !felt.type
// CHECK-NEXT:      constrain.eq %[[V11]], %[[V15]] : !felt.type, !felt.type
// CHECK-NEXT:      constrain.eq %[[V12]], %[[V16]] : !felt.type, !felt.type
// CHECK-NEXT:      constrain.eq %[[V13]], %[[V14]] : !felt.type, !felt.type
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
// -----

!F = !felt.type
module attributes {veridise.lang = "llzk"} {
  struct.def @ArrayMembersAndParams {
    struct.member @arr : !array.type<2,3 x !F>

    function.def @compute(%a: !array.type<2,3 x !F>) -> !struct.type<@ArrayMembersAndParams> {
      %self = struct.new : !struct.type<@ArrayMembersAndParams>
      struct.writem %self[@arr] = %a : !struct.type<@ArrayMembersAndParams>, !array.type<2,3 x !F>
      function.return %self : !struct.type<@ArrayMembersAndParams>
    }

    function.def @constrain(%self: !struct.type<@ArrayMembersAndParams>, %b: !array.type<2,3 x !F>) {
      %a = struct.readm %self[@arr] : !struct.type<@ArrayMembersAndParams>, !array.type<2,3 x !F>

      %0 = arith.constant 0 : index
      %1 = arith.constant 1 : index
      %2 = arith.constant 2 : index

      %a0 = array.read %a[%0, %0] : !array.type<2,3 x !F>, !F
      %b0 = array.read %b[%0, %0] : !array.type<2,3 x !F>, !F
      constrain.eq %a0, %b0 : !F

      %a1 = array.read %a[%0, %1] : !array.type<2,3 x !F>, !F
      %b1 = array.read %b[%0, %1] : !array.type<2,3 x !F>, !F
      constrain.eq %a1, %b1 : !F

      %a2 = array.read %a[%0, %2] : !array.type<2,3 x !F>, !F
      %b2 = array.read %b[%0, %2] : !array.type<2,3 x !F>, !F
      constrain.eq %a2, %b2 : !F

      %a3 = array.read %a[%1, %0] : !array.type<2,3 x !F>, !F
      %b3 = array.read %b[%1, %0] : !array.type<2,3 x !F>, !F
      constrain.eq %a3, %b3 : !F

      %a4 = array.read %a[%1, %1] : !array.type<2,3 x !F>, !F
      %b4 = array.read %b[%1, %1] : !array.type<2,3 x !F>, !F
      constrain.eq %a4, %b4 : !F

      %a5 = array.read %a[%1, %2] : !array.type<2,3 x !F>, !F
      %b5 = array.read %b[%1, %2] : !array.type<2,3 x !F>, !F
      constrain.eq %a5, %b5 : !F

      function.return
    }
  }
}
// CHECK-LABEL: struct.def @ArrayMembersAndParams {
// CHECK-NEXT:    struct.member @arr_0 : !felt.type
// CHECK-NEXT:    struct.member @arr_1 : !felt.type
// CHECK-NEXT:    struct.member @arr_2 : !felt.type
// CHECK-NEXT:    struct.member @arr_3 : !felt.type
// CHECK-NEXT:    struct.member @arr_4 : !felt.type
// CHECK-NEXT:    struct.member @arr_5 : !felt.type
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V1:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V2:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:    %[[V3:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V4:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V5:[0-9a-zA-Z_\.]+]]: !felt.type) -> !struct.type<@ArrayMembersAndParams>  attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V6:[0-9a-zA-Z_\.]+]] = struct.new : <@ArrayMembersAndParams>
// CHECK-DAG:       struct.writem %[[V6]][@arr_5] = %[[V5]] : <@ArrayMembersAndParams>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_4] = %[[V4]] : <@ArrayMembersAndParams>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_3] = %[[V3]] : <@ArrayMembersAndParams>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_2] = %[[V2]] : <@ArrayMembersAndParams>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_1] = %[[V1]] : <@ArrayMembersAndParams>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_0] = %[[V0]] : <@ArrayMembersAndParams>, !felt.type
// CHECK-NEXT:      function.return %[[V6]] : !struct.type<@ArrayMembersAndParams>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V7:[0-9a-zA-Z_\.]+]]: !struct.type<@ArrayMembersAndParams>, %[[V8:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V9:[0-9a-zA-Z_\.]+]]: !felt.type,
// CHECK-SAME:    %[[V10:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V11:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V12:[0-9a-zA-Z_\.]+]]: !felt.type, %[[V13:[0-9a-zA-Z_\.]+]]: !felt.type) attributes {function.allow_constraint} {
// CHECK-DAG:       %[[V14:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_5] : <@ArrayMembersAndParams>, !felt.type
// CHECK-DAG:       %[[V15:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_4] : <@ArrayMembersAndParams>, !felt.type
// CHECK-DAG:       %[[V16:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_3] : <@ArrayMembersAndParams>, !felt.type
// CHECK-DAG:       %[[V17:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_2] : <@ArrayMembersAndParams>, !felt.type
// CHECK-DAG:       %[[V18:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_1] : <@ArrayMembersAndParams>, !felt.type
// CHECK-DAG:       %[[V19:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_0] : <@ArrayMembersAndParams>, !felt.type
// CHECK-NEXT:      constrain.eq %[[V19]], %[[V8]] : !felt.type, !felt.type
// CHECK-NEXT:      constrain.eq %[[V18]], %[[V9]] : !felt.type, !felt.type
// CHECK-NEXT:      constrain.eq %[[V17]], %[[V10]] : !felt.type, !felt.type
// CHECK-NEXT:      constrain.eq %[[V16]], %[[V11]] : !felt.type, !felt.type
// CHECK-NEXT:      constrain.eq %[[V15]], %[[V12]] : !felt.type, !felt.type
// CHECK-NEXT:      constrain.eq %[[V14]], %[[V13]] : !felt.type, !felt.type
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
// -----

!F = !felt.type
module attributes {veridise.lang = "llzk"} {
  // Order the structs so that the member ref is converted before the member itself
  struct.def @ComponentA2 {
    function.def @compute(%p: !struct.type<@ComponentA1>) -> !struct.type<@ComponentA2> {
      %self = struct.new : !struct.type<@ComponentA2>
      %r = struct.readm %p[@f1] : !struct.type<@ComponentA1>, !array.type<4,3 x !F>
      function.return %self : !struct.type<@ComponentA2>
    }

    function.def @constrain(%self: !struct.type<@ComponentA2>, %p: !struct.type<@ComponentA1>) {
      function.return
    }
  }

  struct.def @ComponentA1 {
    struct.member @f1 : !array.type<4,3 x !F> {llzk.pub}

    function.def @constrain(%self: !struct.type<@ComponentA1>) { function.return }
    function.def @compute() -> !struct.type<@ComponentA1> {
      %self = struct.new : !struct.type<@ComponentA1>
      function.return %self : !struct.type<@ComponentA1>
    }
  }
}
// CHECK-LABEL: struct.def @ComponentA2 {
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !struct.type<@ComponentA1>) -> !struct.type<@ComponentA2> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@ComponentA2>
// CHECK-DAG:       %[[V2:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_8] : <@ComponentA1>, !felt.type
// CHECK-DAG:       %[[V3:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_7] : <@ComponentA1>, !felt.type
// CHECK-DAG:       %[[V4:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_10] : <@ComponentA1>, !felt.type
// CHECK-DAG:       %[[V5:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_9] : <@ComponentA1>, !felt.type
// CHECK-DAG:       %[[V6:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_11] : <@ComponentA1>, !felt.type
// CHECK-DAG:       %[[V7:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_2] : <@ComponentA1>, !felt.type
// CHECK-DAG:       %[[V8:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_3] : <@ComponentA1>, !felt.type
// CHECK-DAG:       %[[V9:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_4] : <@ComponentA1>, !felt.type
// CHECK-DAG:       %[[V10:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_5] : <@ComponentA1>, !felt.type
// CHECK-DAG:       %[[V11:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_6] : <@ComponentA1>, !felt.type
// CHECK-DAG:       %[[V12:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_0] : <@ComponentA1>, !felt.type
// CHECK-DAG:       %[[V13:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1_1] : <@ComponentA1>, !felt.type
// CHECK-NEXT:      function.return %[[V1]] : !struct.type<@ComponentA2>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V14:[0-9a-zA-Z_\.]+]]: !struct.type<@ComponentA2>, %[[V15:[0-9a-zA-Z_\.]+]]: !struct.type<@ComponentA1>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
//
// CHECK-LABEL: struct.def @ComponentA1 {
// CHECK-DAG:     struct.member @f1_0 : !felt.type
// CHECK-DAG:     struct.member @f1_1 : !felt.type
// CHECK-DAG:     struct.member @f1_2 : !felt.type
// CHECK-DAG:     struct.member @f1_3 : !felt.type
// CHECK-DAG:     struct.member @f1_4 : !felt.type
// CHECK-DAG:     struct.member @f1_5 : !felt.type
// CHECK-DAG:     struct.member @f1_6 : !felt.type
// CHECK-DAG:     struct.member @f1_7 : !felt.type
// CHECK-DAG:     struct.member @f1_8 : !felt.type
// CHECK-DAG:     struct.member @f1_9 : !felt.type
// CHECK-DAG:     struct.member @f1_10 : !felt.type
// CHECK-DAG:     struct.member @f1_11 : !felt.type
// CHECK-NEXT:    function.def @constrain(%[[V0:[0-9a-zA-Z_\.]+]]: !struct.type<@ComponentA1>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @compute() -> !struct.type<@ComponentA1> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@ComponentA1>
// CHECK-NEXT:      function.return %[[V1]] : !struct.type<@ComponentA1>
// CHECK-NEXT:    }
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @new_then_insert() -> !felt.type {
    %a = felt.const 45
    %b = felt.const 214

    %x = array.new : !array.type<4,2 x !felt.type>
    %y = array.new %a, %b : !array.type<2 x !felt.type>

    %0 = arith.constant 0 : index
    %1 = arith.constant 1 : index
    %2 = arith.constant 2 : index
    array.insert %x[%2] = %y : !array.type<4,2 x !felt.type>, !array.type<2 x !felt.type>

    %z = array.read %x[%2,%1] : !array.type<4,2 x !felt.type>, !felt.type // %b
    function.return %z : !felt.type
  }
}
// CHECK-LABEL: function.def @new_then_insert() -> !felt.type {
// CHECK-NEXT:    %[[V0:[0-9a-zA-Z_\.]+]] = felt.const  214
// CHECK-NEXT:    function.return %[[V0]] : !felt.type
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @new_then_insert_then_insert() -> !array.type<2,2,2 x !felt.type> {
    %a = felt.const 45
    %b = felt.const 214
    %c = felt.const 3
    %d = felt.const 52
    %e = felt.const 99
    %f = felt.const 73

    %r = array.new : !array.type<2,2,2 x !felt.type>
    %s = array.new : !array.type<2,2 x !felt.type>
    %t = array.new %a, %b : !array.type<2 x !felt.type>
    %u = array.new %c, %d : !array.type<2 x !felt.type>
    %v = array.new %e, %f : !array.type<2 x !felt.type>

    %0 = arith.constant 0 : index
    %1 = arith.constant 1 : index

    // populate part of %s and assign at position 1 in %r
    // %r = [[[?, ?], [?, ?]], [[a, b], [?, ?]]]
    array.insert %s[%0] = %t : !array.type<2,2 x !felt.type>, !array.type<2 x !felt.type>
    array.insert %r[%1] = %s : !array.type<2,2,2 x !felt.type>, !array.type<2,2 x !felt.type>

    // overwrite values in %s and assign at position 0 in %r
    // %r = [[[c, d], [e, f]], [[a, b], [?, ?]]]
    array.insert %s[%0] = %u : !array.type<2,2 x !felt.type>, !array.type<2 x !felt.type>
    array.insert %s[%1] = %v : !array.type<2,2 x !felt.type>, !array.type<2 x !felt.type>
    array.insert %r[%0] = %s : !array.type<2,2,2 x !felt.type>, !array.type<2,2 x !felt.type>

    function.return %r : !array.type<2,2,2 x !felt.type>
  }
}
// CHECK-LABEL: function.def @new_then_insert_then_insert() -> (!felt.type, !felt.type, !felt.type, !felt.type, !felt.type, !felt.type, !felt.type, !felt.type) {
// CHECK-DAG:     %[[V0:[0-9a-zA-Z_\.]+]] = felt.const  73
// CHECK-DAG:     %[[V1:[0-9a-zA-Z_\.]+]] = felt.const  99
// CHECK-DAG:     %[[V2:[0-9a-zA-Z_\.]+]] = felt.const  52
// CHECK-DAG:     %[[V3:[0-9a-zA-Z_\.]+]] = felt.const  3
// CHECK-DAG:     %[[V4:[0-9a-zA-Z_\.]+]] = felt.const  214
// CHECK-DAG:     %[[V5:[0-9a-zA-Z_\.]+]] = felt.const  45
// CHECK-DAG:     %[[V6:[0-9a-zA-Z_\.]+]] = llzk.nondet : !felt.type
// CHECK-DAG:     %[[V7:[0-9a-zA-Z_\.]+]] = llzk.nondet : !felt.type
// CHECK-NEXT:    function.return %[[V3]], %[[V2]], %[[V1]], %[[V0]], %[[V5]], %[[V4]], %[[V6]], %[[V7]]
// CHECK-SAME:           : !felt.type, !felt.type, !felt.type, !felt.type, !felt.type, !felt.type, !felt.type, !felt.type
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @new_then_extract() -> !array.type<3,1 x !felt.type> {
    %a = felt.const 45
    %b = felt.const 214
    %c = felt.const 2
    %d = felt.const 52
    %e = felt.const 92
    %f = felt.const 10

    %r = array.new %a, %b, %c, %d, %e, %f : !array.type<2,3,1 x !felt.type>

    %1 = arith.constant 1 : index
    %x = array.extract %r[%1] : !array.type<2,3,1 x !felt.type>
    function.return %x : !array.type<3,1 x !felt.type>
  }
}
// CHECK-LABEL: function.def @new_then_extract() -> (!felt.type, !felt.type, !felt.type) {
// CHECK-DAG:     %[[V0:[0-9a-zA-Z_\.]+]] = felt.const  52
// CHECK-DAG:     %[[V1:[0-9a-zA-Z_\.]+]] = felt.const  92
// CHECK-DAG:     %[[V2:[0-9a-zA-Z_\.]+]] = felt.const  10
// CHECK-NEXT:    function.return %[[V0]], %[[V1]], %[[V2]] : !felt.type, !felt.type, !felt.type
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @new_then_extract_then_read() -> !felt.type {
    %a = felt.const 45
    %b = felt.const 214
    %c = felt.const 2
    %d = felt.const 52
    %e = felt.const 92
    %f = felt.const 10

    %r = array.new %a, %b, %c, %d, %e, %f : !array.type<2,3,1 x !felt.type>

    %0 = arith.constant 0 : index
    %1 = arith.constant 1 : index
    %x = array.extract %r[%1] : !array.type<2,3,1 x !felt.type>
    %z = array.read %x[%0,%0] : !array.type<3,1 x !felt.type>, !felt.type
    function.return %z: !felt.type
  }
}
// CHECK-LABEL: function.def @new_then_extract_then_read() -> !felt.type {
// CHECK-NEXT:    %[[V0:[0-9a-zA-Z_\.]+]] = felt.const  52
// CHECK-NEXT:    function.return %[[V0]] : !felt.type
// CHECK-NEXT:  }
// -----

module attributes {veridise.lang = "llzk"} {
  function.def @new_then_extract_then_extract_then_read() -> !felt.type {
    %a = felt.const 45
    %b = felt.const 214
    %c = felt.const 2
    %d = felt.const 52
    %e = felt.const 92
    %f = felt.const 10

    %r = array.new %a, %b, %c, %d, %e, %f : !array.type<2,3,1 x !felt.type>

    %1 = arith.constant 1 : index
    %x = array.extract %r[%1] : !array.type<2,3,1 x !felt.type>
    %0 = arith.constant 0 : index
    %y = array.extract %x[%0] : !array.type<3,1 x !felt.type>
    %z = array.read %y[%0] : !array.type<1 x !felt.type>, !felt.type
    function.return %z: !felt.type // %d = 52
  }
}
// CHECK-LABEL: function.def @new_then_extract_then_extract_then_read() -> !felt.type {
// CHECK-NEXT:    %[[V0:[0-9a-zA-Z_\.]+]] = felt.const  52
// CHECK-NEXT:    function.return %[[V0]] : !felt.type
// CHECK-NEXT:  }
// -----

// MemberDef and MemberRead w/ non-static shape (i.e., cannot be flattened to scalars)
module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentB2<[@N]> {
    function.def @compute(%p: !struct.type<@ComponentB1<[@N]>>) -> !struct.type<@ComponentB2<[@N]>> {
      %self = struct.new : !struct.type<@ComponentB2<[@N]>>
      %r = struct.readm %p[@f1] : !struct.type<@ComponentB1<[@N]>>, !array.type<4,@N x !felt.type>
      function.return %self : !struct.type<@ComponentB2<[@N]>>
    }

    function.def @constrain(%self: !struct.type<@ComponentB2<[@N]>>, %p: !struct.type<@ComponentB1<[@N]>>) {
      function.return
    }
  }

  struct.def @ComponentB1<[@N]> {
    struct.member @f1 : !array.type<4,@N x !felt.type> {llzk.pub}

    function.def @constrain(%self: !struct.type<@ComponentB1<[@N]>>) { function.return }
    function.def @compute() -> !struct.type<@ComponentB1<[@N]>> {
      %self = struct.new : !struct.type<@ComponentB1<[@N]>>
      function.return %self : !struct.type<@ComponentB1<[@N]>>
    }
  }
}
// CHECK-LABEL: struct.def @ComponentB2<[@N]> {
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !struct.type<@ComponentB1<[@N]>>) -> !struct.type<@ComponentB2<[@N]>> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@ComponentB2<[@N]>>
// CHECK-NEXT:      %[[V2:[0-9a-zA-Z_\.]+]] = struct.readm %[[V0]][@f1] : <@ComponentB1<[@N]>>, !array.type<4,@N x !felt.type>
// CHECK-NEXT:      function.return %[[V1]] : !struct.type<@ComponentB2<[@N]>>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V3:[0-9a-zA-Z_\.]+]]: !struct.type<@ComponentB2<[@N]>>, %[[V4:[0-9a-zA-Z_\.]+]]: !struct.type<@ComponentB1<[@N]>>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }

// CHECK-LABEL: struct.def @ComponentB1<[@N]> {
// CHECK-NEXT:    struct.member @f1 : !array.type<4,@N x !felt.type>
// CHECK-NEXT:    function.def @constrain(%[[V0:[0-9a-zA-Z_\.]+]]: !struct.type<@ComponentB1<[@N]>>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @compute() -> !struct.type<@ComponentB1<[@N]>> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@ComponentB1<[@N]>>
// CHECK-NEXT:      function.return %[[V1]] : !struct.type<@ComponentB1<[@N]>>
// CHECK-NEXT:    }
// CHECK-NEXT:  }
// -----

// Func and CallOp w/ non-static shape (i.e., cannot be flattened to scalars)
module attributes {veridise.lang = "llzk"} {
  struct.def @ComponentC1<[@N]> {
    function.def @compute(%p: !array.type<@N x !felt.type>) -> !struct.type<@ComponentC1<[@N]>> {
      %self = struct.new : !struct.type<@ComponentC1<[@N]>>
      function.return %self : !struct.type<@ComponentC1<[@N]>>
    }

    function.def @constrain(%self: !struct.type<@ComponentC1<[@N]>>, %p: !array.type<@N x !felt.type>) {
      function.return
    }
  }

  struct.def @ComponentC2<[@N]> {
    struct.member @f1 : !struct.type<@ComponentC1<[@N]>>

    function.def @compute(%p: !array.type<@N x !felt.type>) -> !struct.type<@ComponentC2<[@N]>> {
      %self = struct.new : !struct.type<@ComponentC2<[@N]>>
      %c = function.call @ComponentC1::@compute(%p) : (!array.type<@N x !felt.type>) -> !struct.type<@ComponentC1<[@N]>>
      struct.writem %self[@f1] = %c : !struct.type<@ComponentC2<[@N]>>, !struct.type<@ComponentC1<[@N]>>
      function.return %self : !struct.type<@ComponentC2<[@N]>>
    }

    function.def @constrain(%self: !struct.type<@ComponentC2<[@N]>>, %p: !array.type<@N x !felt.type>) {
      function.return
    }
  }
}
// CHECK-LABEL: struct.def @ComponentC1<[@N]> {
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !array.type<@N x !felt.type>) -> !struct.type<@ComponentC1<[@N]>> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@ComponentC1<[@N]>>
// CHECK-NEXT:      function.return %[[V1]] : !struct.type<@ComponentC1<[@N]>>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V2:[0-9a-zA-Z_\.]+]]: !struct.type<@ComponentC1<[@N]>>, %[[V3:[0-9a-zA-Z_\.]+]]: !array.type<@N x !felt.type>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }

// CHECK-LABEL: struct.def @ComponentC2<[@N]> {
// CHECK-NEXT:    struct.member @f1 : !struct.type<@ComponentC1<[@N]>>
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !array.type<@N x !felt.type>) -> !struct.type<@ComponentC2<[@N]>> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V1:[0-9a-zA-Z_\.]+]] = struct.new : <@ComponentC2<[@N]>>
// CHECK-NEXT:      %[[V2:[0-9a-zA-Z_\.]+]] = function.call @ComponentC1::@compute(%[[V0]]) : (!array.type<@N x !felt.type>) -> !struct.type<@ComponentC1<[@N]>>
// CHECK-NEXT:      struct.writem %[[V1]][@f1] = %[[V2]] : <@ComponentC2<[@N]>>, !struct.type<@ComponentC1<[@N]>>
// CHECK-NEXT:      function.return %[[V1]] : !struct.type<@ComponentC2<[@N]>>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V3:[0-9a-zA-Z_\.]+]]: !struct.type<@ComponentC2<[@N]>>, %[[V4:[0-9a-zA-Z_\.]+]]: !array.type<@N x !felt.type>) attributes {function.allow_constraint} {
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
// -----

!F = !felt.type
module attributes {veridise.lang = "llzk"} {
  struct.def @ArrayMembersAndParamsWithPubModifier {
    struct.member @arr : !array.type<2,3 x !F> {llzk.pub}

    function.def @compute(%a: !array.type<2,3 x !F> {llzk.pub}) -> !struct.type<@ArrayMembersAndParamsWithPubModifier> {
      %self = struct.new : !struct.type<@ArrayMembersAndParamsWithPubModifier>
      struct.writem %self[@arr] = %a : !struct.type<@ArrayMembersAndParamsWithPubModifier>, !array.type<2,3 x !F>
      function.return %self : !struct.type<@ArrayMembersAndParamsWithPubModifier>
    }

    function.def @constrain(%self: !struct.type<@ArrayMembersAndParamsWithPubModifier>, %b: !array.type<2,3 x !F> {llzk.pub}) {
      %a = struct.readm %self[@arr] : !struct.type<@ArrayMembersAndParamsWithPubModifier>, !array.type<2,3 x !F>
      function.return
    }
  }
}
// CHECK-LABEL: struct.def @ArrayMembersAndParamsWithPubModifier {
// CHECK-NEXT:    struct.member @arr_0 : !felt.type {llzk.pub}
// CHECK-NEXT:    struct.member @arr_1 : !felt.type {llzk.pub}
// CHECK-NEXT:    struct.member @arr_2 : !felt.type {llzk.pub}
// CHECK-NEXT:    struct.member @arr_3 : !felt.type {llzk.pub}
// CHECK-NEXT:    struct.member @arr_4 : !felt.type {llzk.pub}
// CHECK-NEXT:    struct.member @arr_5 : !felt.type {llzk.pub}
// CHECK-NEXT:    function.def @compute(%[[V0:[0-9a-zA-Z_\.]+]]: !felt.type {llzk.pub}, %[[V1:[0-9a-zA-Z_\.]+]]: !felt.type {llzk.pub}, %[[V2:[0-9a-zA-Z_\.]+]]: !felt.type {llzk.pub},
// CHECK-SAME:    %[[V3:[0-9a-zA-Z_\.]+]]: !felt.type {llzk.pub}, %[[V4:[0-9a-zA-Z_\.]+]]: !felt.type {llzk.pub}, %[[V5:[0-9a-zA-Z_\.]+]]:
// CHECK-SAME:    !felt.type {llzk.pub}) -> !struct.type<@ArrayMembersAndParamsWithPubModifier> attributes {function.allow_witness} {
// CHECK-NEXT:      %[[V6:[0-9a-zA-Z_\.]+]] = struct.new : <@ArrayMembersAndParamsWithPubModifier>
// CHECK-DAG:       struct.writem %[[V6]][@arr_2] = %[[V2]] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_3] = %[[V3]] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_4] = %[[V4]] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_0] = %[[V0]] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_1] = %[[V1]] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-DAG:       struct.writem %[[V6]][@arr_5] = %[[V5]] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-NEXT:      function.return %[[V6]] : !struct.type<@ArrayMembersAndParamsWithPubModifier>
// CHECK-NEXT:    }
// CHECK-NEXT:    function.def @constrain(%[[V7:[0-9a-zA-Z_\.]+]]: !struct.type<@ArrayMembersAndParamsWithPubModifier>, %[[V8:[0-9a-zA-Z_\.]+]]: !felt.type {llzk.pub},
// CHECK-SAME:    %[[V9:[0-9a-zA-Z_\.]+]]: !felt.type {llzk.pub}, %[[V10:[0-9a-zA-Z_\.]+]]: !felt.type {llzk.pub}, %[[V11:[0-9a-zA-Z_\.]+]]: !felt.type {llzk.pub},
// CHECK-SAME:    %[[V12:[0-9a-zA-Z_\.]+]]: !felt.type {llzk.pub}, %[[V13:[0-9a-zA-Z_\.]+]]: !felt.type {llzk.pub}) attributes {function.allow_constraint} {
// CHECK-DAG:       %[[V14:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_2] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-DAG:       %[[V15:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_3] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-DAG:       %[[V16:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_4] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-DAG:       %[[V17:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_0] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-DAG:       %[[V18:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_1] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-DAG:       %[[V19:[0-9a-zA-Z_\.]+]] = struct.readm %[[V7]][@arr_5] : <@ArrayMembersAndParamsWithPubModifier>, !felt.type
// CHECK-NEXT:      function.return
// CHECK-NEXT:    }
// CHECK-NEXT:  }
